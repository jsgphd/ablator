	PROGRAM ABLATOR

C ENERGY-BASED WALL RESPONSE MODEL
C 1-D TRANSIENT HEAT CONDUCTION
C 1-D FINITE DIFFERENCE WAVE PROPAGATION
C TEMPERATURE-DEPENDENT MATERIAL PROPERTIES
C HEAT GENERATION FROM X-RAY DEPOSITION
C IN A DOUBLE EXPONENTIAL PULSE
C OR IN SQUARE OR GAUSSIAN PULSES
C USES 45 BIN APPROXIMATION TO SPECTRUM
C SUPPLY COLD OPACITIES IN INVERSE METERS
C EXPLICIT SCHEME
C SIMPLE RATIO ZONING
C UNITS: SI, KEV FOR BBT STUFF

	IMPLICIT DOUBLE PRECISION(A-H,O-Z)
	PARAMETER (N=400,NP1=401,NBIN=45,NSAVE=5)
	DIMENSION H(N), HG(N), T(0:NP1), TNEW(0:NP1), ZMASS(NP1)
	DIMENSION OPAC(NBIN), BIN(NBIN), BINW(NBIN)
	DIMENSION EFRAC(N,NBIN), EZONE(N)
	DIMENSION ELEAK(NBIN), EIN(NBIN), ELEAKOLD(NBIN)
	DIMENSION FBBLEAK(NBIN), XLOC(NP1)
	DIMENSION ACOND(13), AH2T(10), EOSCOEF(10), SIGMACOEF(7)
	DIMENSION YIELDCOEF(3)
	DIMENSION A(0:N), U(0:NP1), X(0:NP1), X0(0:NP1), XC(NP1)
	DIMENSION XNEW(0:N), SX(0:NP1), SXNEW(0:NP1), P(0:N)
	DIMENSION SXD(N), SXDNEW(N), SZD(N), SZDNEW(N), PHI(0:NP1)
	DIMENSION RHO(0:NP1), RHONEW(0:NP1), Q(0:NP1), C(N)
	DIMENSION XQUAL(NP1), VOLF(NP1)
	DIMENSION RATEJ(N), TNUC(N), BUBBLE(N)
	DIMENSION QP1(N), QP2(N), SURFCSTUFF(10)
	DIMENSION PLASTIC(N), RATIO(N), TPP(N), TPPP(N), TP(N)
	DIMENSION HINP(N),HCONDP(N),HDEVP(N),HSTRESSP(N),HSTRESSQP(N)
	DIMENSION F1P(N),F2P(N),DELRHOP(N)
	INTEGER GEOM, QFLAG, PLASTIC, USEYIELDFLAG
	CHARACTER*1 TAB
	CHARACTER*1 JUNK
	CHARACTER*50 MATNAME
	DOUBLE PRECISION MOMSUM, MYERF


	EXTERNAL TSURF, PSURF, ZBRENT, CONDSURF, QTWO, ERF

	COMMON/PULSE/TSQ,ESQ,EGAUSS,SC,FWHM
	COMMON/MATDAT/TMELT,HMELTI,HMELTT,ACOND,AH2T
	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF
	COMMON/EINTEGRAL/ELEAKOLD,ELEAK,EIN
	COMMON/OPACBINS/BIN,BINW
	COMMON/SURFT/HGSURF,QUALSURFT
	COMMON/SURFP/RHOSURF,QUALSURFP,HGVSURF,HGLSURF,TEMPSURF
	COMMON/SURFC/SURFCSTUFF
	COMMON/SURFTEN/SIGMACOEF

C DEFINITION OF ARRAY VARIABLES
C  H		TOTAL INTERNAL ENERGY IN ZONE
C  HG		SPECIFIC INTERNAL ENERGY FOR A ZONE
C  T		TEMPERATURE IN A ZONE
C  TNEW		NEW TEMPERATURE (TO BE APPLIED NEXT TIME STEP)
C  ZMASS	MASS IN A ZONE (CONSTANT THROUGH PROBLEM)
C  EFRAC	FRACTION OF INCIDENT BIN ENERGY EIN(J) ABSORBED IN A ZONE
C  OPAC		OPACITY IN INVERSE METERS AT A GIVEN PHOTON ENERGY
C  BIN		CENTRAL ENERGY (IN KEV) OF A PHOTON BIN
C  BINW 	PHOTON BIN WIDTH
C  ELEAK	RUNNING SUM OF LEAK ENERGY IN A BIN
C  ELEAKOLD	SUM FROM THE LAST TIME STEP
C  EIN		TOTAL ENERGY ADDED THIS STEP IN A BIN
C  EZONE	FRACTION OF EIN DEPOSITED IN A ZONE
C  FBBLEAK	FRACTION OF LEAK ENERGY IN A BIN
C  XLOC		CENTER OF ORIGINAL ZONE (MICRONS)
C  ACOND	COEFFICIENTS FOR THERMAL CONDUCTIVITY VS TEMP
C  AH2T		COEFFICIENTS FOR TEMPERATURE-ETHALPY RELATIONS
C  EOSCOEF	COEFFICIENTS FOR EQUATIONS OF STATE
C  SIGMACOEF	COEFFICIENTS FOR SURFACE TENSION (AKA SIGMA)
C  A		ACCELERATION OF A NODE
C  U		VELOCITY OF A NODE
C  X		LOCATION OF A NODE
C  X0		ORIGINAL LOCATION OF A NODE
C  XC		CURRENT CENTER LOCATION OF A ZONE
C  XNEW		NEW NODE LOCATION (TO BE APPLIED NEXT TIME STEP)
C  SX		NORMAL STRESS
C  SXNEW	NEW NORMAL STRESS (TO BE APPLIED NEXT TIME STEP)
C  P		PRESSURE
C  SXD		DEVIATORIC STRESS
C  SXDNEW	NEW DEVIATORIC STRESS (TO BE APPLIED NEXT TIME STEP)
C  SZD		DEVIATORIC STRESS (FOR CYLINDRICAL GEOMETRIES)
C  SZDNEW	NEW DEVIATORIC STRESS (TO BE APPLIED NEXT TIME STEP)
C  PHI		DIFFERENCE BETWEEN LONGITUDINAL AND TRANSVERSE STRESS
C  RHO		DENSITY
C  RHONEW	NEW DENSITY (TO BE APPLIED NEXT TIME STEP)
C  Q		ARTIFICIAL VISCOSITY
C  C		LOCAL SOUND SPEED
C  XQUAL	QUALITY (VAPOR MASS FRACTION) IN A ZONE
C  VOLF		VAPOR VOLUME FRACTION IN A ZONE
C  RATEJ	LOG OF BUBBLE NUCLEATION RATE
C  TNUC		LOG OF INDUCTION TIME FOR NUCLEATION RATE DEVELOPMENT (STEADY?)
C  BUBBLE	LOG OF CUMULATIVE NUMBER OF BUBBLES PER UNIT VOLUME
C  PLASTIC	FLAG 1 FOR ZONE WHERE PLASTICITY OCCURED, OTHERWISE 0
C  QP1		QUANTITY FOR MOSS & WHITE ARTIFICIAL VISCOSITY
C  QP2		QUANTITY FOR MOSS & WHITE ARTIFICIAL VISCOSITY
C  SURFCSTUFF	QUANTITIES FOR SURFACE ZONE CONDUCTION ITERATIONS


C DEFINE A BLACKBODY FUNCTION
	BBE(JBB,TBB) = BINW(JBB) * ((BIN(JBB)/TBB)**3) / 
     &		(DEXP(BIN(JBB)/TBB)-1.)
	TAB = CHAR(9)
	PI = 4.*ATAN(1.)

C READ RADIATION SOURCE DATA FROM A FILE
	OPEN(UNIT=9,FILE="initdata", STATUS="OLD")

C	WRITE(*,*) 'BB X-ray or laser source? (1=Laser)'
C	READ(*,*) LASERFLAG
	READ(9,130) COMMENT
	READ(9,*) LASERFLAG

	IF (LASERFLAG .NE. 1) THEN
		OPEN(UNIT=8,FILE="opacdata",STATUS="OLD")
		READ(8,130) MATNAME
		WRITE(*,*) MATNAME
		DO 2, J=1,NBIN
			READ(8,101) BIN(J),JUNK,BINW(J),JUNK,OPAC(J)
2		CONTINUE
		CLOSE(8)
C SKIP READING ABS.COEFF. FROM THE FILE
		READ(9,130) COMMENT
		READ(9,130) COMMENT

	ELSE
C		WRITE(*,*) 'Enter absorbtion coefficient in reciprocal microns: '
C		READ(*,*) OPACLASER
		READ(9,130) COMMENT
		READ(9,*) OPACLASER

		DO 3, J=1,NBIN
			BIN(J) = 0
			BINW(J) = 0
			OPAC(J) = OPACLASER * 1D+06
3		CONTINUE
	ENDIF

C READ IN MATERIAL PROPERTIES
	CALL READMAT
	GAMMA = EOSCOEF(9)
	RBAR = EOSCOEF(10)

C SHEAR MODULUS COEFFICIENT, BASED ON POISSON'S RATIO
	GNU = (1.-2.*PRNU) / (2.*(1.-PRNU))
C SOUND SPEED COEFFICIENT, BASED ON POISSONS' RATIO
	SNU = SQRT(3.* (1.-PRNU)/(1.+PRNU))

C CHOOSE ARTIFICIAL VISCOSITY METHOD
c WRITE(*,*) 'Artificial viscosity method'
c WRITE(*,*) '	1 = VNR'
c WRITE(*,*) '	2 = M&W'
c READ(*,*) QFLAG
	QFLAG = 1

C INITIALIZE ARTIFICIAL VISCOSITY COEFFICIENTS
C B1, B2 ARE FOR SOLID, B3 FOR SPALLED MATERIAL (NOT USED)
	B1 = 1.000
	B2 = 0.300
C	B3 = 0.060

C INITIALIZE TIME STEP CONTROL COEFFICIENTS
	CT1 = 0.9
	CT2 = 1.1
C	CFL MUST BE < 0.5
	CFL = 0.4

C SET SPALL STRESS (FOR REMOVAL OF MELTED MATERIAL) (in Pa)
	SPALL = -1.0D+10

C SET UP ZONES
C GEOM IS FLAG FOR PLANAR (1), CYLINDRICAL (2) OR SPHERICAL (3)
	CALL ZONESET(RHO0,X0,XC,XLOC,ZMASS,GEOM)
	IF (GEOM .EQ. 1) WRITE(*,*) 'PLANAR'	
	IF (GEOM .EQ. 2) WRITE(*,*) 'CYLINDRICAL'
	IF (GEOM .EQ. 3) WRITE(*,*) 'SPHERICAL'
	IF (GEOM .LT. 1 .OR. GEOM .GT. 3) THEN
		WRITE(*,*) 'GEOM = ', GEOM
		STOP
	ENDIF

C CHOOSE SOURCE TERM
7	CONTINUE
C	WRITE(*,*) 'Choose pulse type'
C	WRITE(*,*) '1 = square pulse'
C	WRITE(*,*) '2 = Gaussian pulse'
C	READ(*,*) ISOURCE
	READ(9,130) COMMENT
	READ(9,*) ISOURCE

	IF (ISOURCE .GT. 2 .OR. ISOURCE .LT. 1) GOTO 7

C SET UP FOR ENERGY DEPOSITION BASED ON ISOURCE CHOICE
	CALL XSOURCE(ISOURCE,LASERFLAG,FBBLEAK,TSTART)
	
C	WRITE(*,*) 'Enter radius (microns)'
C	READ(*,*) RADIUS
	READ(9,130) COMMENT
	READ(9,*) RADIUS
	AREA = PI * (RADIUS * 1D-06)**2

	READ(9,130) COMMENT
	READ(9,*) USEYIELDFLAG
	write(*,*) 'yield: ',USEYIELDFLAG
	
	LHYDRO = 1

	HG0 = 0.0
	TINF=AH2T(1)

C DETEREMINE INITIAL TIME STEP
C ROOM TEMPERATURE MATERIAL PROPERTIES, TO GET THINGS STARTED
C FIRST GET HEAT CAPACITY AT ROOM TEMPERATURE
	CALL H2T(HG0,T0)
	H1 = HG0 + 0.1
	CALL H2T(H1,T1)
	CVS = (H1 - HG0) / (T1 - T0)
C NOW GET HEAT CAPACITY OF LIQUID
	CVL = 1. / AH2T(8)
C CHOOSE MINIMUM FOR USE IN TIME STEP CALCULATION
	CV0 = MIN(CVS,CVL)

C J/kg.K
      VASAN = 0.0
	CALL CONDVST(TINF,VASAN,COND)
C W/m.K
	ALPHA = COND / (RHO0 * CV0)
C m2/sec
	DTHEAT = CFL* (X0(1)-X0(0))**2 / ALPHA

C SET PROBLEM TIME (sec) AND FLAGS FOR OCCASSIONAL DATA DUMPS (ns)
C	WRITE(*,*) 'Enter Tstop (ns)'
C	READ (*,*) TSTOP
	READ(9,130) COMMENT
	READ(9,*) TSTOP
	CLOSE(9)
	TM = TSTART * 1.0D-09
	TSTOP = TSTOP * 1.0D-09 + 2.*DTHEAT
	TFRONT = TSTART - 1.0D-13 
	TSCREEN = TSTART - 1.0D-13

	XMELTMAX = 0.
	XTMELTMAX = 0.
	TFMAX = TINF
	HGFMAX = HG0
	DHGMXMAX = 0.0

C INITIALIZE ENERGIES AND TEMPERATURES
C ZONE ENERGY STORED IN H(), SPECIFIC ENERGY STORED IN HG()
	DO 13, I=1,N
		HINP(I) = 0.0
		HCONDP(I) = 0.0
		HDEVP(I) = 0.0
		HSTRESSP(I) = 0.0
		HSTRESSQP(I) = 0.0
		H(I) = HG0 * ZMASS(I)
		HG(I) = HG0
		T(I) = TINF
13	CONTINUE
	T(0) = TINF
	T(NP1) = TINF

C INITIALIZE HYDRO-MOTION VARIABLES
	CALL EOSF(RHO0,F1,F2)
	P0 = F1 + F2 * HG0
	CALL SOUND(HG0,RHO0,P0,F2,C0)
C CONVERT SOUND TO ELASTIC FROM HYDRODYNAMIC MATERIAL
	C0 = C0 * SNU
	DO 221, I=1,N
		A(I) = 0.00D+00
		U(I) = 0.00D+00
		X(I) = X0(I)
		RHO(I) = RHO0
		Q(I) = 0.00D+00
		P(I) = P0
		SX(I) = 0.00D+00
		SXD(I) = 0.00D+00
		PHI(I) = 0.00D+00
		C(I) = C0
		XQUAL(I) = 0.00D+00
		VOLF(I) = 0.00D+00
		QP1(I) = 0.0D+00
		QP2(I) = 0.0D+00
		BUBBLE(I) = 0.0D+00
221	CONTINUE

C ASSUME FREE BOUNDARY AT FRONT
	U(0) = 0.00D+00
	X(0) = X0(0)
	RHO(0) = 0.00D+00
	RHONEW(0) = 0.00D+00
	SX(0) = 0.00D+00
	P(0) = 0.00D+00
	PHI(0) = 0.00D+00
	Q(0) = 0.00D+00
C ASSUME NON-REFLECTING BOUNDARY AT BACK
	U(NP1) = 0.00D+00
	UNOLD = 0.00D+00
	X(NP1) = X0(NP1)
	RHO(NP1) = 0.00D+00
	SX(NP1) = 0.00D+00
	PHI(NP1) = 0.00D+00
	Q(NP1) = 0.00D+00
	XQUAL(NP1) = 0.00D+00
	VOLF(NP1) = 0.00D+00

C INITIALIZE LIQUID DENSITY (FOR SURFACE VAPORIZATION)
	RHOLSURF = RHO0

C SET INITIAL SPALL ZONE AND FRONT SURFACE ZONE
	ISPALL = 0
	ISURF = 1

C SET MIN QUALITY FOR SURFACE ZONE TO BE CONSIDERED W/O VAPOR
	QMIN = 1.0D-06

C SET INITIAL TIME STEP SIZE
	DTHYDRO = (X0(1)-X0(0))/C0
	DT = MIN(DTHEAT,DTHYDRO)
	IF (LHYDRO .NE. 1) DT = DTHEAT
	DTOLD = DT

C SET UP SOURCE ENERGY MULTIPLIERS
C WILL GIVE FRACTION OF ENERGY IN EACH ZONE, IN EACH BIN
C OF THE TOTAL INCIDENT FROM WALL AND LEAK X RAYS
C SCALE LEAK ENERGY WITH LAMBERTIAN DISTRIBUTION
	DO 15, J=1,NBIN
		FRACIN = 1.
		DO 14, I=1,N
			FRACOUT = EXP(-X(I) * OPAC(J))
			EFRAC(I,J) = FRACIN - FRACOUT
			FRACIN = FRACOUT
14		CONTINUE

C INITIALIZE ENERGIES/BIN FOR FIRST TIME STEP
		ELEAKOLD(J) = 0.
C		WRITE(*,*) EFRAC(1,J)
15	CONTINUE
	DO J=1,NBIN
		SUMEFRAC = 0.
		DO I=1,N
			SUMEFRAC = SUMEFRAC + EFRAC(I,J)
		END DO
C		WRITE(*,*) J,SUMEFRAC
	END DO

C SAVE ENERGY FRACTION IN EACH ZONE
	DO 17, I=1,N
		EZONE(I) = 0.0D+00
		DO 16, J=1,NBIN
			EZONE(I) = EZONE(I) + EFRAC(I,J)
16		CONTINUE
17	CONTINUE

C	SUMALEAK = 0.
C	WRITE(*,*) '3.FBBLEAK(J):'
C	DO J=1,NBIN
C		SUMALEAK = SUMALEAK + FBBLEAK(J)
C		WRITE(*,*) FBBLEAK(J)
C	END DO
C	WRITE(*,*) '3.SUMALEAK=',SUMALEAK

C FOR LASER SOURCE, RECORD ENERGY DEPOSITION PROFILE
	IF (LASERFLAG .EQ. 1) THEN
		OPEN(UNIT=10,FILE='ldepfrac')
		DO 18, I=1,N
			WRITE (10,111) I, X0(I)*1.0D+06, EFRAC(I,1)
18		CONTINUE
		CLOSE(10)
	END IF

	OPEN(UNIT=10,FILE='Pfront')
	OPEN(UNIT=11,FILE='Pprofile')
	OPEN(UNIT=13,FILE='SXProfile')
	OPEN(UNIT=14,FILE='SXDprofile') 
	OPEN(UNIT=27,FILE='Qprofile')
	OPEN(UNIT=26,FILE='Aprofile')
	OPEN(UNIT=15,FILE='Uprofile')
	OPEN(UNIT=16,FILE='Xprofile')
	OPEN(UNIT=17,FILE='RHOprofile')
	OPEN(UNIT=18,FILE='QUALprofile')
	OPEN(UNIT=19,FILE='Tprofile')
	OPEN(UNIT=20,FILE='HGprofile')
	OPEN(UNIT=21,FILE='spall')
	OPEN(UNIT=22,FILE='VOLFprofile')
	OPEN(UNIT=24,FILE='TNUCprofile')
	OPEN(UNIT=25,FILE='Jprofile')
	OPEN(UNIT=35,FILE='BUBprofile')
	OPEN(UNIT=28,FILE='PLASTICprofile')
	OPEN(UNIT=29,FILE='RATIOprofile')
	OPEN(UNIT=30,FILE='TPPprofile')
	OPEN(UNIT=31,FILE='TPPPprofile')
	OPEN(UNIT=32,FILE='EINprofile')
	OPEN(UNIT=33,FILE='TPprofile')
	OPEN(UNIT=34,FILE='ESUMDIFprofile')
	OPEN(UNIT=36,FILE='ESUMprofile')
	OPEN(UNIT=37,FILE='HINprofile')
	OPEN(UNIT=38,FILE='HCONDprofile')
	OPEN(UNIT=39,FILE='HDEVprofile')
	OPEN(UNIT=40,FILE='HSTRESSprofile')
	OPEN(UNIT=41,FILE='F1profile')
	OPEN(UNIT=42,FILE='F2profile')
	OPEN(UNIT=43,FILE='DELRHOprofile')
	OPEN(UNIT=44,FILE='EKINprofile')
	OPEN(UNIT=45,FILE='ESUMVAPprofile')
	OPEN(UNIT=46,FILE='ESUMSLprofile')
	OPEN(UNIT=47,FILE='ESUMDIFVAPprofile')
	OPEN(UNIT=48,FILE='ESUMDIFSLprofile')
	OPEN(UNIT=49,FILE='MOMprofile')

	ISURFEND = 23
	EKINSIGN = 1
	EINMAX = 0.
	EINMAXVAP = 0.
	EINMAXSL = 0.
	DTMIN = 100.
	DTMAX = 0.0
	HEASTMAX = 0.0
	DHGMXMAX = 0.0
	IF (GEOM .GT. 1 .AND. X0(0) .EQ. 0.0) THEN
		IFLAGAXIS = 1
	ELSE
		IFLAGAXIS = 0
	END IF
	
C INITIALIZE MAX BUBBLE NUCLEATION DEPTHS AND ZONE NUMBERS
	I35 = 1
	I30 = 1
	I25 = 1
	I20 = 1
	X35 = 0.0
	X30 = 0.0
	X25 = 0.0
	X20 = 0.0
	IB27 = 1
	IB26 = 1
	IB25 = 1
	IB24 = 1
	XB27 = 0.0
	XB26 = 0.0
	XB25 = 0.0
	XB24 = 0.0
C INIT PLASTICITY FLAG
	PLASTICFLAG = 0

C SET UP TO PRINT INFO TO SCREEN EVERY NCYCLEPRINT CYCLES
	NCYCLEPRINT = 10000
	NCYCLE = 1
	TPRESS = TM * 1.0D+09
	TPRESSEIN = TPRESS

C *******************************************************
C START MARCHING IN TM
20	CONTINUE
	TM = TM + DT
	IF (TM .GT. TSTOP) GOTO 95

C SET UP MAGNITUDE OF ENERGY PULSES
C RETURNS ENERGY IN EACH BIN FOR THIS TIME STEP, EIN(J) IN COMMON
	CALL XPULSE(TM,ISOURCE,AREA,FBBLEAK)

C ADD THE TOTAL ENERGY DEPOSITED THIS STEP TO RUNNING TOTAL
	EINDT=0.0
	DO 39, J=1,NBIN
		EINMAX = EINMAX + EIN(J)
		EINDT = EINDT + EIN(J)
39	CONTINUE

C RESET MAX SPECIFIC ENERGY CHANGE
	DHGMAX = 0.0
C RESET SPALL FLAG
	ISPALLFLAG = 0

C SET UP THERMAL CONDUCTIVITIES FOR THE FIRST ZONE
	VFLAG = VOLF(1)
	IF (ISURF .EQ. 1) VFLAG = 0.0D+00
	CALL CONDVST(T(1),VFLAG,CONDE)
	CALL BETAGEOM(GEOM,X(0),X(1),BETAC)
	AE = CONDE * BETAC / (X(1) - X(0))
C INSULATED FRONT SURFACE (CONDUCTION BETWEEN ZONES 0 AND 1)
	HEAST = 0.0D+00
C SET PLASTICITY FLAG FOR EACH ZONE
	DO I=1,N
		PLASTIC(I) = 0
	ENDDO

	IF (LHYDRO .EQ. 1) THEN
C SET UP FOR FINDING NEW HYDRO-BASED MAXIMUM DT
	DTHYDRO = 100.
	IF (ISURF .EQ. 1 .AND. XQUAL(ISURF) .GT. QMIN) THEN
C DETERMINE LEFT NODE MOTION OF SURF ZONE W/O F=MA
		TRIGHT = T(1)
		PRIGHT = P(1)
		PLEFT = P(0)
		CALL USURFIX(TRIGHT,PRIGHT,PLEFT,UNEW)
		A(0) = (UNEW - U(0)) / DT
		U(0) = UNEW
		XNEW(0) = X(0) + DT * U(0)
	ELSE
		IF (GEOM .EQ. 1) THEN
C DETERMINE ACCELERATION, VELOCITY, LOCATION FOR LEFT EDGE
			A(0) = 2. * (0.0 - (SX(1)+Q(1))) /
     &				(RHO(1) * (X(1) - X(0)) + 0.0)
			U(0) = U(0) + A(0) * (DT + DTOLD)/2.
			XNEW(0) = X(0) + DT * U(0)
		ELSE
C CHECK IF INNER RADIUS HAS ALREADY HIT AXIS
			IF (IFLAGAXIS .EQ. 0) THEN
				A(0) = 2. * (0. - (SX(1)+Q(1))) /
     &					(RHO(1)*(X(1)+X(0)) + 0.) + 
     &					2. * (GEOM-1) * (PHI(1) + PHI(0)) / 
     &					(RHO(1)*(X(1) + X(0)) + 0.)
				UNEW = U(0) + A(0) * (DT + DTOLD)/2.
				XNEW0 = X(0) + DT * UNEW
C CHECK IF SURFACE HAS PASSED THROUGH AXIS
				IF (XNEW0 .LE. 0.0) THEN
C KEEPING SAME ACCELERATION, FIGURE DT TO WHEN STUFF HITS AT CENTER
					CALL AXIS(X(0),U(0),A(0),DTOLD,DTNEW)
					DT = DTNEW
					XNEW(0) = 0.0
					U(0) = 0.0
					IFLAGAXIS = 1
				ELSE
					U(0) = UNEW
					XNEW(0) = XNEW0
				END IF
			END IF
		END IF
	END IF
	END IF


C %%%%%%%%%%%%%%%%%%%%%% START OF LOOP THROUGH ZONES %%%%%%%%%%%%%%%%%%%%%%
C FRONT BC IS THERMALLY INSULATED
C REAR BC IS AT FIXED TEMPERATURE
C FRONT SURFACE IS FREE TO MOVE
C REAR SURFACE IS NON-REFLECTING

	DO 50, I=1,N
	IF (LHYDRO .EQ. 1) THEN
C COMPUTE ACCELERATION, VELOCITY, LOCATION
		IF (I .EQ. (ISURF-1) .AND. XQUAL(ISURF) .GT. QMIN .AND. 
     &			VOLF(I) .GT. 0.99) THEN
C DETERMINE RIGHT NODE MOTION OF ISURF ZONE W/ ADJUSTED PRESSURE
			PRIGHT = P(ISURF)
			DXSURF = (X(ISURF)-X(I)) * (1.0D+00-VOLF(ISURF))
			XINTERNAL = X(ISURF) - DXSURF
			DXC1 = XINTERNAL - XC(I)
			DX1 = XINTERNAL - X(I)
			CALL PLEFTCALC(PRIGHT,P(I),DXC1,DX1,RHOVSURF,
     &				XQUAL(ISURF),T(ISURF),PLEFT)
			A(I) = 2. * ((SX(I)+Q(I)) - (PLEFT+Q(I+1))) /
     &				(RHO(I+1)*(X(I+1)-X(I)) + RHO(I)*(X(I)-X(I-1))) + 
     &				2.* (GEOM-1) * (PHI(I) - PHI(I+1)) / 
     &				(RHO(I+1)*(X(I+1)+X(I)) + RHO(I)*(X(I-1)+X(I)))
			U(I) = U(I) + A(I) * (DT+DTOLD)/2.

		ELSE IF (I .LT. N) THEN
			A(I) = 2. * ((SX(I)+Q(I)) - (SX(I+1)+Q(I+1))) / 
     &				(RHO(I+1)*(X(I+1)-X(I)) + RHO(I)*(X(I)-X(I-1))) + 
     &				2.* (GEOM-1) * (PHI(I) - PHI(I+1)) / 
     &				(RHO(I+1)*(X(I+1)+X(I)) + RHO(I)*(X(I-1)+X(I)))
			U(I) = U(I) + A(I) * (DT+DTOLD)/2.
		ELSE
C NON-REFLECTING BOUNDARY CONDITION (NRBC)
C BASED ON HALPERN, 1982
			DXNRBC = X(N) - X(N-1)
			TNRBC0 = 2.*((C(N)*DT)**2) / (C(N)*DT+DXNRBC)
			TNRBC1 = (-1./DXNRBC) + (DXNRBC/(C(N)*DT)**2)
			TNRBC2 = (C(N)*DT-DXNRBC)/(2.*(C(N)*DT)**2)
			TNRBC3 = 1./DXNRBC
			UNEW = TNRBC0*(U(N)*TNRBC1+UNOLD*TNRBC2+U(N-1)*TNRBC3)
			UNOLD = U(N)
			U(N) = UNEW
			A(N) = U(N) / DT
		END IF
		
		XNEW(I) = X(I) + DT * U(I)
C COMPUTE NEW DENSITY FROM ZONE MASS
		CALL DENGEOM(GEOM,XNEW(I-1),XNEW(I),RDEN)
		RHONEW(I) = ZMASS(I) / ((XNEW(I) - XNEW(I-1)) * RDEN)
		RHOBAR = (RHONEW(I) + RHO(I)) / 2.
		DELRHO = 0.5 * (RHONEW(I)-RHO(I))/RHOBAR**2
		RHODOT = (RHONEW(I)-RHO(I))/(DT*RHOBAR)

C COMPUTE FACTORS FOR LINEAR (IN ENERGY) EOS
		RHOEOS = RHONEW(I)
		IF (I .EQ. ISURF) THEN
C SPECIAL TREATMENT FOR VAPORIZING SURFACE ZONE
			CALL FSURF(RHOEOS,RHOLSURF,XQUAL(ISURF),QMIN,F1,F2)
		ELSE IF (I .GT. ISPALL) THEN
C SOLID/LIQUID ZONE
			CALL EOSF(RHOEOS,F1,F2)
		ELSE IF (VOLF(I) .LT. 0.001) THEN
			CALL EOSF(RHOEOS,F1,F2)
		ELSE IF (VOLF(I) .GT. 0.999) THEN
			CALL FVAPOR(RHOEOS,F1,F2)
		ELSE
			XQ2 = XQUAL(I)
			CALL F2PHASE(RHOEOS,HG(I),XQ2,F1,F2)
		END IF

COMPUTE ARTIFICIAL VISCOSITY
		XXXX = (XNEW(I) - XNEW(I-1) + X(I) - X(I-1)) / 2.
		QLIN = RHOBAR * B2 * XXXX * C(I) * RHODOT
		QQUAD = RHOBAR * (B1 * XXXX)**2 * RHODOT * ABS(RHODOT)
C CHOOSE VON NEUMAN & RICHTMEYER OR MOOS & WHITE FORM
		IF (QFLAG .EQ. 2) THEN
C SET CRITERION FROM MOOS & WHITE
			QP = SX(I) * (U(I)-U(I-1))/XXXX
			QPTEST = QP1(I) + (QP1(I)-QP2(I)) * DT/DTOLD
			QF1 = 0.0
			QF2 = 0.0
			IF (QP .GT. 0.) QF1=1.
			IF (QP .GT. QPTEST) QF2=1.
			Q(I) = (QQUAD + QLIN) * (QF1 + QF2)
		ELSE
C VON NEUMANN & RICHTMEYER
			IF (RHODOT .GT. 0.) THEN
				Q(I) = QLIN + QQUAD
			ELSE
				Q(I) = 0.0D+00
			END IF
		END IF
C TURN OFF ART VISC FOR SURFACE ZONE
		IF (I .EQ. (ISURF)) THEN
			Q(I) = 0.0D+00
		END IF

C COMPUTE DEVIATORIC STRESSES AND ENERGIES IF SOLID
C		IF (T(I) .LT. TMELT .AND. I .GT. ISPALL) THEN
		IF (0. GT. 1.) THEN
			DXD = (U(I) - U(I-1)) / XXXX + RHODOT / 3.
			G = GNU * RHO(I) * C(I)**2
C COMPUTE CURRENT YIELD STRENGTH 
			YIELDFLAG = YIELDCOEF(1)
			IF (YIELDFLAG .EQ. 1) THEN
C USING LINEAR RAMP OF YIELD STRENGTH FROM YIELD0 @ TINF DOWN TO 0 @ TMELT
				YIELD0 = YIELDCOEF(2)
				YBIG = YIELD0 * (TMELT-T(I))/(TMELT-TINF)
			ELSE IF (YIELDFLAG .EQ. 2) THEN
C USING EXPONENTIAL FIT
				CYIELD = YIELDCOEF(2)
				EYIELD = YIELDCOEF(3)
				YBIG = CYIELD * DEXP(EYIELD/T(I))
			END IF
			SIGMAXD = SXD(I) + 2. * DT * G * DXD

C SPLIT BASED ON GEOMETRY
			IF (GEOM .EQ. 1 .OR. GEOM .EQ. 3) THEN
C PLANAR OR SPHERICAL
				YIELDTEST = YBIG * 2./3.
				IF (ABS(SIGMAXD) .GT. YIELDTEST) THEN
					PLASTICFLAG = 1
					PLASTIC(I) = 1
C					WRITE(*,*) 'PLASTICITY: ',I,TM
					IF (USEYIELDFLAG .EQ. 1) THEN
						SXDNEW(I) = ABS(SIGMAXD)/SIGMAXD * YIELDTEST
					ELSE
						SXDNEW(I) = SIGMAXD
					END IF
				ELSE
					SXDNEW(I) = SIGMAXD
				END IF
				PHI(I) = 1.5 * SXDNEW(I)
				DELED = 0.75*DT*DXD*(SXDNEW(I)+SXD(I)) / RHOBAR
			ELSE
C CYLINDRICAL
				DZD = RHODOT / 3.
				SIGMAZD = SZD(I) + 2. * DT * G* DZD
				FY = 2.*(SIGMAXD**2 + SIGMAXD*SIGMAZD + SIGMAZD**2)
				YIELDTEST = (2./3.) * YBIG**2
				IF (ABS(SIGMAXD) .GT. YIELDTEST) THEN
					PLASTICFLAG = 1
					PLASTIC(I) = 1
					IF (USEYIELDFLAG .EQ. 1) THEN
						FSQ = SQRT(YIELDTEST/FY)
						SXDNEW(I) = SIGMAXD * FSQ
						SZDNEW(I) = SIGMAZD * FSQ
					END IF
				ELSE
					FSQ = SQRT(YIELDTEST/FY)
					SXDNEW(I) = SIGMAXD * FSQ
					SZDNEW(I) = SIGMAZD * FSQ
				END IF
				DELED = (DT/(2.*RHOBAR)) * ((SXDNEW(I)+SXD(I))*(2.*DXD+DZD)+
     &					(SZDNEW(I)+SZD(I))*(2.*DZD+DZD))
				PHI(I) = 2.*SXDNEW(I) + SZDNEW(I)
			END IF
		ELSE
C MELTED OR SPALLED MATERIALS
			DELED = 0.0D+00
			PHI(I) = 0.0D+00
			SXDNEW(I) = 0.0D+00
			SZDNEW(I) = 0.0D+00
		END IF
		IF (I .LE. ISPALL) SXDNEW(I) = 0.0

C COMPUTE STRESS ENERGY CHANGE IN J/m2
		HSTRESS = (F1 + P(I) + 2.*Q(I)) * DELRHO * ZMASS(I)
		HSTRESSQ = (F1 + P(I)) * DELRHO * ZMASS(I)

C COMPUTE DEVIATORIC CONTRIBUTION IN J/m2
		HDEV = DELED * ZMASS(I)
	END IF
C END IF FOR LHYDRO-ONLY STEPS

C SET UP SOURCE ENERGY
C SUM TO GET NET ENERGY/ZONE
C NET ENERGY HAS UNITS OF J/m2
	HIN = 0.
	DO 48, J=1,NBIN
		HIN = HIN + EFRAC(I,J) * EIN(J)
48	CONTINUE
	IF (I .LE. ISURFEND) THEN
		EINMAXVAP = EINMAXVAP + HIN
	ELSE
		EINMAXSL = EINMAXSL + HIN
	END IF

C HEAT CONDUCTION BALANCE ( (I-1) TO EAST = -(I TO WEST) )
	HWEST = HEAST
C SET CONDUCTION COEFFICIENT FOR THIS ZONE
	CONDP = CONDE
	VFLAG = VOLF(I+1)
	IF (I .EQ. (ISURF-1)) VFLAG = 0.0D+00
	CALL CONDVST(T(I+1),VFLAG,CONDE)
	CONDEI = 2. * CONDP * CONDE / (CONDP + CONDE)

C BASE HEAT CONDUCTION ON TEMP, LOCATIONS, PROPERTIES FROM PREVIOUS TM
	IF (I .EQ. ISURF) THEN
C ADJUST HEAT CONDUCTION DISTANCE TO REFLECT ONLY REMAINING LIQUID
C AND SOLVE IMPLICITLY
		DXSURF = (X(I)-X(I-1)) * (1.0D+00-VOLF(I))
		XINTERNAL = X(I) - DXSURF
		CALL BETAGEOM(GEOM,XINTERNAL,X(I),BETAC)
		DXP1 = XC(I+2) - XC(I+1)
C FIGURE WHAT WILL BE HEAT COND BETWEEN (I+1) AND (I+2)
		CALL CONDVST(T(I+2),VOLF(I+2),CONDE2)
		CONDEI1 = 2. * CONDE * CONDE2 / (CONDE + CONDE2)
		CALL BETAGEOM(GEOM,XC(I+1),XC(I+2),BETAC1)
		AE1 = CONDEI1 * BETAC1 / DXP1
		HEAST1 = AE1*(T(I+1)-T(I+2))
C SET TEMPORARY ENERGIES IN ZONE ISURF AND ISURF+1
C + HSTRESS
		HTEMPP = H(ISURF) + HWEST * DT + HIN + HSTRESS
		HTEMPE = H(ISURF+1) - HEAST1*DT + HIN*EZONE(I+1)/EZONE(I)
		HGTEMPP = HTEMPP / ZMASS(ISURF)
		HGTEMPE = HTEMPE / ZMASS(ISURF+1)
C ENHANCE CONDUCTIVITY TO REDUCE SURFACE TEMPERATURE VARIATIONS
		CONDP = CONDP * (1.0D+00 + 4.*XQUAL(ISURF)**2)
C FILL COMMON BLOCKS FOR ITERATION ROUTINE
		SURFCSTUFF(1) = HTEMPP
		SURFCSTUFF(2) = HTEMPE
		SURFCSTUFF(3) = ZMASS(ISURF)
		SURFCSTUFF(4) = ZMASS(ISURF+1)
		SURFCSTUFF(5) = DT
		SURFCSTUFF(6) = QMIN
		SURFCSTUFF(7) = CONDP
		SURFCSTUFF(8) = DXSURF
		SURFCSTUFF(9) = BETAC
		SURFCSTUFF(10) = T(I)
		QUALSURFT = XQUAL(I)
C SET UP BRACKETING INITIAL GUESSES FOR HEAST BASED ON CURRENT TEMPS
		HEAST = CONDP * BETAC * (T(I)-T(I+1)) / DXSURF

		CALL BRACKET(CONDSURF,HEAST,HE1,HE2)
		TOL = 1.0D-05
		HEAST = ZBRENT(CONDSURF,HE1,HE2,TOL)
	ELSE
		CALL BETAGEOM(GEOM,XC(I),XC(I+1),BETAC)
		DXC = XC(I+1) - XC(I)
		AE = CONDEI * BETAC / DXC
		HEAST = AE*(T(I)-T(I+1))
	END IF
	IF (HEAST .GT. HEASTMAX) HEASTMAX = HEAST
C COMPUTE NET ENERGY CHANGE FROM CONDUCTION
	HCOND = (HWEST - HEAST) * DT

C UPDATE TOTAL ZONE AND SPECIFIC ENERGIES
C + HSTRESS
	DH = (HIN+HCOND+HDEV+HSTRESS) / (1.0D+00 - F2*DELRHO)
	IF (LHYDRO .NE. 1) DH = HIN + HCOND
	H(I) = H(I) + DH
	HG(I) = H(I) / ZMASS(I)
C STORE EACH ENERGY CONTRIBUTION FOR PRINTING
	HINP(I) = HINP(I) + HIN / (1.0D+00 - F2*DELRHO)
	HCONDP(I) = HCONDP(I) + HCOND / (1.0D+00 - F2*DELRHO)
	HDEVP(I) = HDEVP(I) + HDEV / (1.0D+00 - F2*DELRHO)
	HSTRESSP(I) = HSTRESSP(I) + HSTRESS / (1.0D+00 - F2*DELRHO)
	HSTRESSQP(I) = HSTRESSQP(I) + HSTRESSQ / (1.0D+00 - F2*DELRHO)

C	HSTRESSP(I) = (F1 + P(I) + 2.*Q(I)) * (0.5 / RHOBAR) * ZMASS(I) / (1.0D+00 - F2*DELRHO)

C CHECK FOR MAXIMUM CHANGE IN ZONE ENERGY/kg
	DHG = DH / ZMASS(I)
	IF (DABS(DHG) .GT. DHGMAX) THEN
		DHGMAX = DABS(DHG)
		IDHGMAX = I
		IF (DHGMAX .GT. DHGMXMAX) THEN
			DHGMXMAX = DHGMAX
			IDHGMXMAX = I
		END IF
	END IF

C APPLY EOS INFORMATION TO GET PRESSURE, TEMP, SOUND SPEED
C DIFFERENT TREATMENTS DEPENDING ON EXPECTED STATE

C SPECIAL TREATMENT FOR SURFACE ZONE (PRESCRIBED QUALITY FROM SURFACE VAPORIZATION ROUTINE)
	IF (I .EQ. ISURF) THEN
		TEMPSURF = T(I)
		HGSURF = HG(I)
		QUALSURFT = XQUAL(I)
		RHOSURF = RHONEW(I)
		QUALSURFP = XQUAL(I)
		PM1 = P(I-1)
		CALL EOSURF(RHOLSURF,RHOVSURF,RHONEW(I+1),QMIN,
     &			PM1,P(I),VOLF(I),C(I))
		TNEW(I) = TEMPSURF
		IF (TNEW(I) .LT. 0.0) THEN
			WRITE (*,*) 'ERROR IN SURF TEMP'
			WRITE (*,*) 'T = ',TNEW(I)
			WRITE (*,*) 'I = ', I
			WRITE (*,*) 'TIME = ',TM*1.0D+09
			WRITE (*,*) 'HG (cal/g) = ',HT
			STOP
		END IF
	
C USE SOLID/LIQUID EOS FOR UNSPALLED MATERIAL
	ELSE IF (I .GT. ISPALL) THEN
C DETERMINE WHAT THE NEW TEMPERATURE WILL BE
		HT = HG(I)
		CALL H2T(HT,TNEW(I))
		IF (TNEW(I) .LT. 0.0) THEN
			WRITE (*,*) 'ERROR IN SOLID/LIQUID TEMP'
			WRITE (*,*) 'T = ',TNEW(I)
			WRITE (*,*) 'I = ', I
			WRITE (*,*) 'TIME = ',TM*1.0D+09
			WRITE (*,*) 'HG (cal/g) = ',HT
			STOP
		END IF

C COMPUTE NEW PRESSURE FROM EOS
		P(I) = F1 + F2 * HG(I)

C COMPUTE NEW SOUND SPEED
		HEOS = HG(I)
		REOS = RHONEW(I)
		PEOS = P(I)
		CALL SOUND(HEOS,REOS,PEOS,F2,CEOS)
		IF (TNEW(I) .LT. TMELT) THEN
C APPLY CORRECTION FOR ELASTIC MATERIAL
			C(I) = SNU * CEOS
		ELSE
			C(I) = CEOS
		END IF

C USE "SPALL" OR "VAPOR" EOS FOR SPALLED MATERIAL
	ELSE
		TS = T(I)
		IF (HG(I) .GT. 0.0 .AND. HG(I) .LT. 1.0D+15) THEN
			CALL EOSPALL(HG(I),RHONEW(I),TS,P(I),XQUAL(I),VOLF(I),C(I))
			TNEW(I) = TS
			IF (TNEW(I) .LT. 0.0) THEN
				WRITE (*,*) 'ERROR IN SPALL TEMP'
				WRITE (*,*) 'T = ',TNEW(I)
				WRITE (*,*) 'I = ', I
				WRITE (*,*) 'TIME = ',TM*1.0D+09
				WRITE (*,*) 'HG (cal/g) = ',HG(I)
				WRITE (*,*) 'RHO = ',RHONEW(I)
				STOP
			END IF
		ELSE
			WRITE (*,*) 'ERROR IN ZONE ENERGY'
			WRITE (*,*) 'HG (cal/g) = ',HG(I)/4184
			WRITE (*,*) 'I = ', I
			WRITE (*,*) 'TIME = ',TM*1.0D+09
			WRITE (*,*) 'T = ',T(I)
			WRITE (*,*) 'RHO = ',RHONEW(I)
			WRITE (*,*) 'QUAL = ',XQUAL(I)
			STOP
		END IF
	END IF

C COMPUTE NEW SIGMA-X
	SXNEW(I) = P(I) - SXDNEW(I)

C COMPUTE MAXIMUM HYDRO TIME STEP FOR THIS ZONE
	DELTAX = XNEW(I) - XNEW(I-1)
	DTI = CT1 * DELTAX / 
     &		(B2*C(I) + 2.*B1**2 * ABS(RHODOT) * DELTAX + 
     &		SQRT((B2*C(I) + 2.*B1**2 * ABS(RHODOT) * DELTAX)**2 + 
     &		C(I)**2))
	IF (DTI .LT. DTHYDRO) THEN
		DTHYDRO = DTI
		IHYDRO = I
	END IF

	TNS = TM * 1.0D+09

C UPDATE POWERS FOR ARTIFICIAL VISCOSITY SWITCH
	QP2(I) = QP1(I)
	QP1(I) = QP
	
	IF (LHYDRO .EQ. 1) THEN
C CHECK FOR SPALLED ZONE (LIQUID AT NEGATIVE STRESS)
		TTEST = TMELT + 1.0D-05
		IF (TNEW(I) .GE. TTEST .AND. I .GT. ISPALL) THEN
			IF (SXNEW(I) .LT. SPALL) THEN
				ISPALL = I
				WRITE(*,*) 'ISPALL melt=',ISPALL
				WRITE(21,134) TM*1.0D+09,TAB,ISPALL,TAB,
     &					X0(ISPALL)*1.0D+06
				ISURF = ISPALL+1
				ISPALLFLAG = 1
			END IF
		END IF
	END IF
	F1P(I) = F1
	F2P(I) = F2
	DELRHOP(I) = DELRHO
	
50	CONTINUE
C %%%%%%%%%%%%%%%%%%%%%%% END OF LOOP THROUGH ZONES %%%%%%%%%%%%%%%%%%%%%%%


C UPDATE OLD T, X, XC,RHO, AND SIGMA VALUES
	X(0) = XNEW(0)
	T(0) = T(1)
	DTEMPMAX = 0.0
	DO 57, I=1,N
C STORE FOR LIMITING TIME STEP
		DTEMP = ABS(T(I)-TNEW(I))
		IF (DTEMP .GT. DTEMPMAX) THEN
			DTEMPMAX = DTEMP
			ITEMPMAX = I
		END IF
		T(I) = TNEW(I)
		X(I) = XNEW(I)
		XC(I) = (X(I) + X(I-1)) / 2.
		RHO(I) = RHONEW(I)
		SX(I) = SXNEW(I)
		SXD(I) = SXDNEW(I)
C *** TO JSEM PRIDAL JA ***
		SZD(I) = SZDNEW(I)
57	CONTINUE
	T(N) = TINF

C DETERMINE MELT DEPTH, IF ANY
	TMLOW = TMELT - 0.1
	TMHIGH = TMELT + 0.1
	IF (T(1) .GE. TMLOW) THEN
		DO 64, I=ISPALL+1,N
			IF (T(I) .LT. TMLOW .AND. T(I-1) .GE. TMLOW) THEN
				XMELT = XLOC(I-1)
			END IF
			IF (T(I) .LT. TMHIGH .AND. T(I-1) .GE. TMHIGH) THEN
				XTMELT = XLOC(I-1)
			END IF
64		CONTINUE
		IF (XMELT .GT. XMELTMAX) XMELTMAX = XMELT
		IF (XTMELT .GT. XTMELTMAX) THEN 
			XTMELTMAX = XTMELT
		END IF
	ELSE
		XMELT = 0.0
		XTMELT = 0.0
	END IF
	
C SURFACE VAPORIZATION SECTION
C COMPUTE VAPORIZED MASS AND ENERGY IN THIS TIME STEP
C FIRST SET AMBIENT PRESSURE
	IF (ISURF .EQ. 1) THEN
		PAMB = 1.33D-04
	ELSE
		PAMB = P(ISURF-1)
	END IF
	DTVAP = 100.
	CALL SURFVAP(GEOM,PAMB,TNEW(ISURF),XNEW(ISURF),DT,
     &		DVAPMASS,DHVAP)

C GET NEW QUALITY (MASS FRACTION OF VAPOR) IN SURFACE ZONE
	DXQ = DVAPMASS / ZMASS(ISURF)
	XQ = XQUAL(ISURF) + DXQ
	
	IF (XQ .LT. 1.0D+00) THEN
		XQUAL(ISURF) = XQ
	ELSE
C SPALL OFF THIS ZONE AND ADVANCE ISURF
		DQI = 1.0D+00 - XQUAL(ISURF)
		XQUAL(ISURF) = 1.0D+00
		VOLF(ISURF) = 1.0D+00
		DVAPMASS = ZMASS(ISURF) * (XQ - 1.0D+00)
		ISPALL = ISURF
		ISURF = ISURF + 1
		WRITE(*,*) 'ISURFvapor =',ISPALL
		WRITE(*,*) 'TIME =',TM*1.0D+09
		WRITE(*,*) 'CYCLE =',NCYCLE
		WRITE(21,134) TM*1.0D+09,TAB,ISPALL,TAB,
     &			X0(ISPALL)*1.0D+06

C SET UP NEXT SURFACE ZONE PROPERLY
		DXQ = DVAPMASS / ZMASS(ISURF)
		XQUAL(ISURF) = DXQ
		RHOV = P(ISURF) / (RBAR * T(ISURF))
C *** TO JSEM ZMENIL JA (VOLF = QUAL * RHO / RHOV) ***
		VOLF(ISURF) = XQUAL(ISURF) * RHO(ISURF) / RHOV
C		VOLF = QUAL * RHO / RHOV
		
		RHOLSURF = RHO(ISURF)
		ISPALLFLAG = 1
	END IF
C END OF SURFACE VAPORIZATION SECTION

C SET NEW TIME STEP TO PREVENT EXCESSIVE VAPORIZATION NEXT STEP
	IF (ISPALLFLAG .EQ. 1) THEN
		CALL SURFVAP(GEOM,PAMB,TNEW(ISURF),XNEW(ISURF),DT,
     &			DVMNEXT,DHVAPN)
		VAPRATIO = DVMNEXT / ZMASS(ISURF)
		IF (VAPRATIO .GT. 0.02) THEN
			DTVAP = DT * 0.02 / VAPRATIO
		END IF
	END IF

C KEEP TRACK OF MAXIMUM FRONT SURFACE ENERGY
	IF (HG(1) .GT. HGFMAX) THEN
		HGFMAX = HG(1)
	END IF

C *** TO JSEM PRIDAL JA, PRI HG(UP) MUZE T(DOWN) (VYPAROVANI) ***
C KEEP TRACK OF MAXIMUM FRONT SURFACE TEMPERATURE
	IF (T(1) .GT. TFMAX) THEN
		TFMAX = T(1)
	END IF

C WRITE SURFACE ZONE PRESSURE TO A FILE FOR A WHILE
	TNS = TM * 1.0D+09
	IF (TNS .LT. 3.) THEN
		WRITE(10,155) TNS,TAB,ISURF,TAB,P(ISURF),
     &			TAB,T(ISURF),TAB,T(ISURF+1)
	END IF

C CHECK ENERGY BALANCE
	HSUM = 0.0
	EKIN = 0.0
	DO 92, I=1,N
C		HSUM = HSUM + H(I) - HSTRESS(I)
          HSUM = HSUM + H(I) - HSTRESS
		UBAR = (U(I) + EKINSIGN*U(I-1)) / 2.
		EKIN = EKIN + 0.5 * ZMASS(I) * UBAR**2
92	CONTINUE
	ESUM = HSUM + EKIN

C OUTPUT FOR DIAGNOSTIC PURPOSES
	NCYMOD = MOD(NCYCLE,NCYCLEPRINT)
	IF (NCYMOD .EQ. 0) THEN
		TNS = TM * 1.0D+09
		WRITE(*,*) 'TIME',TM*1.0D+09
		K1 = ISPALL + 1
		K2 = ISPALL + NSAVE
		WRITE(*,139) 'U		', (U(K),K=K1,K2)
		WRITE(*,139) 'P		', (P(K),K=K1,K2)
		WRITE(*,139) 'T		', (T(K),K=K1,K2)
		WRITE(*,136) ESUM/10000.0, EINMAX/10000.0
	END IF
	NCYCLE = NCYCLE + 1
	
C DETERMINE NEW HEAT TIME STEP FROM CFL CONSTRAINT
	DTHEAT = 1.0D+02
	DO 70, I=1,N
		IF (T(I) .LT. 0.0) THEN
			WRITE(*,*) 'cfl T =',T(I)
			WRITE(*,*) 'I =',I
			WRITE(*,*) 'ISURF=',ISURF
			WRITE(*,*) 'CYCLE=',NCYCLE
			WRITE(*,*) 'HG =',HG
			STOP
		END IF
		VFLAG = VOLF(I)
		IF (I .EQ. ISURF) VFLAG = 0.0D+00
		CALL CONDVST(T(I),VFLAG,COND)
		CALL HEATCAP(VOLF(I),T(I),HG(I),CV)
		ALPHA = COND / (RHO(I) * CV)
		DXHEAT = X(I) - X(I-1)
		DTI = CFL * DXHEAT**2 / ALPHA
		IF (DTI .LT. DTHEAT) THEN
			DTHEAT = DTI
		END IF
70	CONTINUE

C LIMIT TIME STEP IF MAX TEMP CHANGE IN LAST TIME STEP WAS TOO BIG
	TEMPBIG = 200
	IF (DTEMPMAX .GT. TEMPBIG) THEN
		DTTEMP = DT * TEMPBIG / DTEMPMAX
	ELSE
		DTTEMP = 100.0
	END IF

C SET NEXT TIME STEP FROM HYDRO AND THERMAL CONSTRAINTS
	DTOLD = DT
	DT = MIN(DTHEAT,DTHYDRO,CT2*DT,DTVAP,DTTEMP)
	IF (LHYDRO .NE. 1) DT = DTHEAT
	IF (DT .GT. DTMAX) DTMAX = DT
	IF (DT .LT. DTMIN) THEN
		DTMIN = DT
	END IF

C TIME STEP DIAGNOSTIC SECTION
	IF (DT .LT. 1.0D-17) THEN
		WRITE(*,153) DT,DTHEAT,DTHYDRO,DTVAP,DTTEMP,DTEMPMAX
C		WRITE(*,*) ITEMPMAX,T(ITEMPMAX),ABS(T(ITEMPMAX) - TNEW(ITEMPMAX))
	END IF

C COMPUTE NUCLEATION RATE (LOG10(J) = RATEJ)
	DO 87, K=1,N
		IF (K .GT. ISURF .AND. T(K) .GT. TMELT) THEN
			CALL NUCLEATE(T(K),RHO(K),P(K),RAWJ,RAWTNUC)

			RATEJ(K) = DLOG10(RAWJ)
			TNUC(K) = DLOG10(RAWTNUC)
			RAWBUB = 10**BUBBLE(K) + RAWJ*DT
			BUBBLE(K) = DLOG10(RAWBUB)

C TEST VARIOUS MAXIMUM THRESHOLD DEPTHS
			IF (RATEJ(K) .GT. 35. .AND. K .GT. I35) THEN
				I35 = K
				X35 = XLOC(K)
			END IF
			IF (RATEJ(K) .GT. 30. .AND. K .GT. I30) THEN
				I30 = K
				X30 = XLOC(K)
			END IF
			IF (RATEJ(K) .GT. 25. .AND. K .GT. I25) THEN
				I25 = K
				X25 = XLOC(K)
			END IF
			IF (RATEJ(K) .GT. 20. .AND. K .GT. I20) THEN
				I20 = K
				X20 = XLOC(K)
			END IF
			IF (BUBBLE(K) .GT. 27. .AND. K .GT. IB27) THEN
				IB27 = K
				XB27 = XLOC(K)
			END IF
			IF (BUBBLE(K) .GT. 26. .AND. K .GT. IB26) THEN
				IB26 = K
				XB26 = XLOC(K)
			END IF
			IF (BUBBLE(K) .GT. 25. .AND. K .GT. IB25) THEN
				IB25 = K
				XB25 = XLOC(K)
			END IF
			IF (BUBBLE(K) .GT. 24. .AND. K .GT. IB24) THEN
				IB24 = K
				XB24 = XLOC(K)
			END IF
		ELSE
			RATEJ(K) = -99.
		END IF
87	CONTINUE

C WRITE PRESSURE AND STUFF TO FILES EVERY DTPRESS NANOSECONDS
	TNS = TM * 1.0D+09
	
C WRITE THE PULSE ENERGY TO FILE
	DTPRESSEIN=1E-5
	IF (TSQ .GT. 0.0) THEN
		TPULSE = TSQ + 0.04 * TSQ
	ELSE IF (FWHM .GT. 0.0) THEN
		TPULSE = FWHM * 3
	END IF
	IF (TNS .LE. TPULSE) THEN
		IF (TNS .GT. TPRESSEIN) THEN
			WRITE(32,159) TNS,TAB,EINDT*(DTPRESSEIN/(DTOLD*1.0D+09))/10000.0
			TPRESSEIN=TPRESSEIN+DTPRESSEIN
		END IF
	END IF

	IF (TNS .LT. 0.001) THEN
		DTPRESS = 1E-4
	ELSE IF (TNS .LT. 0.01) THEN
		DTPRESS = 5E-4
C	ELSE IF (TNS .LT. 0.1) THEN
C		DTPRESS = 5E-3
	ELSE IF (TNS .LT. 0.5) THEN
		DTPRESS = 1E-3
	ELSE IF (TNS .LT. 1.0) THEN
		DTPRESS = 0.01
	ELSE IF (TNS .LT. 3.0) THEN
		DTPRESS = 0.05
	ELSE IF (TNS .LT. 20.0) THEN
		DTPRESS = 0.5
	ELSE
		DTPRESS = 5.0
	END IF
	
	IF (TNS .GT. TPRESS) THEN
		WRITE(11,105) TNS,(TAB,P(K),K=1,N)
		WRITE(13,105) TNS,(TAB,SX(K),K=1,N)
		WRITE(14,105) TNS,(TAB,SXD(K),K=1,N)
		WRITE(27,105) TNS,(TAB,Q(K),K=1,N)
		WRITE(26,145) TNS,(TAB,A(K),K=0,N)
		WRITE(15,145) TNS,(TAB,U(K),K=0,N)
		WRITE(16,145) TNS,(TAB,XNEW(K),K=0,N)
		WRITE(17,105) TNS,(TAB,RHONEW(K),K=1,N)
		WRITE(18,105) TNS,(TAB,XQUAL(K),K=1,N)
		WRITE(19,105) TNS,(TAB,T(K),K=1,N)
		WRITE(20,105) TNS,(TAB,HG(K),K=1,N)
		WRITE(22,105) TNS,(TAB,VOLF(K),K=1,N)
		WRITE(24,105) TNS,(TAB,TNUC(K),K=1,N)
		WRITE(25,105) TNS,(TAB,RATEJ(K),K=1,N)
		WRITE(35,105) TNS,(TAB,BUBBLE(K),K=1,N)
		WRITE(28,110) TNS,(TAB,PLASTIC(K),K=1,N)
		WRITE(37,105) TNS,(TAB,HINP(K)/10000.0,K=1,N)
		WRITE(38,105) TNS,(TAB,HCONDP(K)/10000.0,K=1,N)
		WRITE(39,105) TNS,(TAB,HDEVP(K)/10000.0,K=1,N)
		WRITE(40,105) TNS,(TAB,HSTRESSP(K)/10000.0,K=1,N)
		WRITE(41,105) TNS,(TAB,F1P(K),K=1,N)
		WRITE(42,105) TNS,(TAB,F2P(K),K=1,N)
		WRITE(43,105) TNS,(TAB,ZMASS(K)/6*(U(K)**2+U(K-1)**2
     &		+ U(K)*U(K-1)),K=1,N)
		WRITE(44,105) TNS,(TAB,(1/6)*ZMASS(K)*(U(K)**2
     &		 + U(K)*U(K-1) + U(K-1)**2) ,K=1,N)

C 3RD AND 2ND T DERIVATIVE RATIO FOR DIAGNOSTICS
		DX = X(I) - X(I-1)
		RELDTLIMIT = 0.001
C FIRST DERIVATIVE
		DO I=2,N-1
			TMV = (T(I+1) + T(I-1)) / 2
			IF (ABS((T(I+1)-T(I-1)) / TMV) .GT. RELDTLIMIT) THEN
				TP(I) = (T(I+1) - T(I-1)) / (2 * DX)
			ELSE
				TP(I) = 0.0
			END IF
		END DO
		TMV = (T(2) + T(1)) / 2
		IF (ABS((T(2)-T(1)) / TMV) .GT. RELDTLIMIT) THEN
			TP(1) = (T(2) - T(1)) / DX
		ELSE
			TP(1) = 0.0
		END IF
		TMV = (T(N) + T(N-1)) / 2
		IF (ABS((T(N)-T(N-1)) / TMV) .GT. RELDTLIMIT) THEN
			TP(N) = (T(N) - T(N-1)) / DX
		ELSE
			TP(N) = 0.0
		END IF

C SECOND DERIVATIVE
		DO I=2,N-1
			TPP(I) = (TP(I+1) - TP(I-1)) / (2 * DX)
		END DO
		TMV = (T(2) + T(1)) / 2
		IF (ABS((T(2)-T(1)) / TMV) .GT. RELDTLIMIT) THEN
			TPP(1) = (T(2) - T(1)) / DX**2
		ELSE 
			TPP(1) = 0.0
		END IF
		TMV = (T(N) + T(N-1)) / 2
		IF (ABS((T(N)-T(N-1)) / TMV) .GT. RELDTLIMIT) THEN
			TPP(N) = (-T(N) + T(N-1)) / DX**2
		ELSE 
			TPP(N) = 0.0
		END IF

C THIRD DERIVATIVE
		DO I=2,N-1
			TPPP(I) = (TPP(I+1) - TPP(I-1)) / (2 * DX)
		END DO
		TPPP(1) = TPP(1) / DX
		TPPP(N) = -TPP(N) / DX

C RATIO
		DO I=1,N
			IF (ABS(TPP(I)) .GT. 0) THEN
				RATIO(I) = ABS((TPPP(I) * DX) / (2 * TPP(I)))
			ELSE 
				RATIO(I) = 0.0
			END IF
		END DO
		WRITE(29,105) TNS,(TAB,RATIO(K),K=1,N)
		WRITE(33,105) TNS,(TAB,TP(K),K=1,N)
		WRITE(30,105) TNS,(TAB,(2 * TPP(K)),K=1,N)
		WRITE(31,105) TNS,(TAB,(TPPP(K) * DX),K=1,N)

C ENERGY BALANCE
C SUM FOR DIFFERENT TYPES + INPUT ENERGY
C FOR VAPORS
		HINSUM = 0.0
		HCONDSUM = 0.0
		HDEVSUM = 0.0
		HSTRESSSUM = 0.0
		EKIN = 0.0
		DO I=1,ISURFEND
			HINSUM = HINSUM + HINP(I)
			HCONDSUM = HCONDSUM + HCONDP(I)
			HDEVSUM = HDEVSUM + HDEVP(I)
			HSTRESSSUM = HSTRESSSUM + HSTRESSP(I)
			UBAR = (U(K)**2 + U(K)*U(K-1) + U(K-1)**2)/6
			EKIN = EKIN + ZMASS(I) * UBAR
		END DO
		WRITE(47,161) TNS,TAB,HINSUM/10000.0,TAB,HCONDSUM/10000.0,TAB,
     &			HDEVSUM/10000.0,TAB,HSTRESSSUM/10000.0,TAB,EKIN/10000.0
C FOR SOLID/LIQUID
		HINSUM = 0.0
		HCONDSUM = 0.0
		HDEVSUM = 0.0
		HSTRESSSUM = 0.0
		EKIN = 0.0
		DO I=ISURFEND+1,N
			HINSUM = HINSUM + HINP(I)
			HCONDSUM = HCONDSUM + HCONDP(I)
			HDEVSUM = HDEVSUM + HDEVP(I)
			HSTRESSSUM = HSTRESSSUM + HSTRESSP(I)
			UBAR = (U(K)**2 + U(K)*U(K-1) + U(K-1)**2)/6
			EKIN = EKIN + ZMASS(I) * UBAR		
		END DO
		WRITE(48,161) TNS,TAB,HINSUM/10000.0,TAB,HCONDSUM/10000.0,TAB,
     &			HDEVSUM/10000.0,TAB,HSTRESSSUM/10000.0,TAB,EKIN/10000.0
C VAPOR + SOLID/LIQUID
		HINSUM = 0.0
		HCONDSUM = 0.0
		HDEVSUM = 0.0
		HSTRESSSUM = 0.0
		EKIN = 0.0
		DO I=1,N
			HINSUM = HINSUM + HINP(I)
			HCONDSUM = HCONDSUM + HCONDP(I)
			HDEVSUM = HDEVSUM + HDEVP(I)
			HSTRESSSUM = HSTRESSSUM + HSTRESSP(I)
			UBAR = (U(K)**2 + U(K)*U(K-1) + U(K-1)**2)/6
			EKIN = EKIN + ZMASS(I) * UBAR
		END DO
		WRITE(34,161) TNS,TAB,HINSUM/10000.0,TAB,HCONDSUM/10000.0,TAB,
     &			HDEVSUM/10000.0,TAB,HSTRESSSUM/10000.0,TAB,EKIN/10000.0


C SUM OF ALL TYPES + INPUT ENERGY
C FOR VAPORS
		ESUM = 0.0
		DO I=1,ISURFEND
			UBAR = (U(I) + EKINSIGN*U(I-1)) / 2.
			EKIN = 0.5 * ZMASS(I) * UBAR**2
			ESUM = ESUM + H(I) - HSTRESSP(I)
		END DO
		WRITE(45,160) TNS,TAB,EINMAXVAP/10000.0,TAB,ESUM/10000.0
C FOR SOLID/LIQUID
		ESUM = 0.0
		DO I=ISURFEND+1,N
			UBAR = (U(I) + EKINSIGN*U(I-1)) / 2.
			EKIN = 0.5 * ZMASS(I) * UBAR**2
			ESUM = ESUM + H(I) - HSTRESSP(I)
		END DO
		WRITE(46,160) TNS,TAB,EINMAXSL/10000.0,TAB,ESUM/10000.0
C VAPOR + SOLID/LIQUID
		ESUM = 0.0
		DO I=1,N
			UBAR = (U(I) + EKINSIGN*U(I-1)) / 2.
			EKIN = 0.5 * ZMASS(I) * UBAR**2
			ESUM = ESUM + H(I) - HSTRESSP(I)
		END DO
		WRITE(36,160) TNS,TAB,EINMAX/10000.0,TAB,ESUM/10000.0

C CHECK THE MOMENTUM CONSERVATION
		MOMSUM = 0.0
		DO I=1,N
			MOMSUM = MOMSUM + (U(I)+U(I-1)/2) * ZMASS(I)
		END DO
		WRITE(49,159) TNS,TAB,MOMSUM

		TPRESS = TPRESS + DTPRESS
	END IF

C WRITE SOME INFO TO SCREEN EVERY FEW NS
	DTSCREEN = 2.
	IF (TNS .GT. 50.) DTSCREEN = 10.
	IF (TNS .GT. TSCREEN) THEN
		K1 = ISURF
		K2 = ISURF + 4
		WRITE(*,137) TNS, DT, (T(K),K=K1,K2), XMELT
		TSCREEN = TSCREEN + DTSCREEN
	END IF

	GOTO 20

C *****************************************************************
C TIME LIMIT HAS BEEN REACHED
95	CONTINUE
	
	CLOSE(10)
	CLOSE(11)
	CLOSE(13)
	CLOSE(14)
	CLOSE(15)
	CLOSE(16)
	CLOSE(17)
	CLOSE(26)
	CLOSE(27)
	CLOSE(18)
	CLOSE(19)
	CLOSE(20)
	CLOSE(21)
	CLOSE(22)
	CLOSE(25)
	CLOSE(35)
	CLOSE(28)
	CLOSE(29)
	CLOSE(30)
	CLOSE(31)
	CLOSE(32)
	CLOSE(33)
	CLOSE(34)
	CLOSE(36)
	CLOSE(37)
	CLOSE(38)
	CLOSE(39)
	CLOSE(40)
	CLOSE(41)
	CLOSE(42)
	CLOSE(43)
	CLOSE(44)
	CLOSE(45)
	CLOSE(46)
	CLOSE(47)
	CLOSE(48)
	CLOSE(49)

	X0M1 = X0(ISURF-1)
	VAPDEPTH = 1.0D+06*(X0M1+XQUAL(ISURF)*(X0(ISURF)-X0M1))
	OPEN(UNIT=23,FILE='summary')
	EINMAX = EINMAX / 10000.
	WRITE(*,112) EINMAX
	WRITE(*,113) XMELTMAX
	WRITE(*,119) XTMELTMAX
	WRITE(*,120) X0(ISPALL) * 1.0D+06
	WRITE(*,114) TFMAX
	WRITE(*,115) HGFMAX / 4184.
	WRITE(*,117) DTMIN, DTMAX
	WRITE(*,118) DHGMXMAX / 4184.
	WRITE(*,122) HEASTMAX / 10000.
	WRITE(*,135) XQUAL(ISURF)
	WRITE(*,136) ESUM/10000., EINMAX
	WRITE(*,148) VAPDEPTH
	WRITE(*,157) X35,X30,X25,X20
	WRITE(*,158) XB27,XB26,XB25,XB24
	IF (PLASTICFLAG	.EQ. 1) THEN 
		WRITE(*,*) 'Plastic deformation has occured!'
	END IF

	WRITE(23,112) EINMAX
	WRITE(23,113) XMELTMAX
	WRITE(23,119) XTMELTMAX
	WRITE(23,120) X0(ISPALL) * 1.0D+06
	WRITE(23,114) TFMAX
	WRITE(23,115) HGFMAX / 4184.
	WRITE(23,117) DTMIN, DTMAX
	WRITE(23,118) DHGMXMAX / 4184.
	WRITE(23,122) HEASTMAX / 10000.
	WRITE(23,135) XQUAL(ISURF)
	WRITE(23,136) ESUM/10000., EINMAX
	WRITE(23,148) VAPDEPTH
	WRITE(23,157) X35,X30,X25,X20
	WRITE(23,158) XB27,XB26,XB25,XB24
	IF (PLASTICFLAG	.EQ. 1) THEN 
		WRITE(23,*) 'Plastic deformation has occured!'
	END IF
	
	CLOSE(23)
	
99	CONTINUE

999	FORMAT(2(' ',E13.6))
101	FORMAT(D13.6,2(A1,D13.6))
102	FORMAT('TIME =',2F12.3)
103	FORMAT(F10.5,A1,F10.2,A1,F12.3,A1,F12.4)
104	FORMAT('TIME =',F12.5)
105	FORMAT(F10.5,400(A1,E12.5E3))
106	FORMAT(F10.5,F10.2,F10.3)
107	FORMAT(F12.5,F12.3,F12.5)
108	FORMAT(F12.3,35(A1,F12.5))
109	FORMAT(4E12.4)
110	FORMAT(F10.5,400(A1,I1))
111	FORMAT(I5,F10.4,E14.5)
112	FORMAT('Total fluence (J/cm2) =',F12.3)
113	FORMAT('Maximum melt depth (micron) =',F12.3)
114	FORMAT('Maximum front surface temp =',F12.2)
115	FORMAT('Maximum front surface enthalpy (cal/g) =',F12.4)
117	FORMAT('Minimum and maximum time steps =',2(1PE12.3))
118	FORMAT('Maximum enthalpy change (cal/g) =',F12.4)
119	FORMAT('Maximum total melt depth (micron) =',F12.3)
120	FORMAT('Spalled depth (micron) =',F12.3)
122	FORMAT('Maximum heat flux (W/cm2) =',1PE12.4)
130	FORMAT(A)
131	FORMAT(D12.4)
133	FORMAT(D13.6,2(A3,D15.6))
134	FORMAT(F10.4,A1,I5,A1,F10.4)
135	FORMAT('Fraction vaporized in surface zone',F10.5)
136	FORMAT('Energy present & input (J/cm2) =',2F12.3)
137	FORMAT(F11.3,E11.3,6F11.3)
138	FORMAT(I8,F8.3,2E12.3,F12.5,I5)
139	FORMAT(A7,5(1PE12.5))
141	FORMAT(F12.4,5(A1,1PE12.4))
145	FORMAT(F10.5,401(A1,E12.5E3))
146	FORMAT(F10.5,A1,I5,2(A1,F12.5))
147	FORMAT(F12.7,4(A1,1PE12.5))
148	FORMAT('Vaporized depth (micron) =',F12.3)
149	FORMAT(F12.7,5(A1,1PE12.5))
153	FORMAT('low DT ',6E12.4)
154	FORMAT(F12.7,A1,1PE12.5)
155	FORMAT(F12.7,A1,I5,3(A1,E12.5))
157	FORMAT('J-depths (35,30,25,20) =',4F10.3)
158	FORMAT('Bubble-depths (27,26,25,24) =',4F10.3)
159	FORMAT(F10.5,A1,E11.5)
160	FORMAT(F10.5,2(A1,F10.6))
161	FORMAT(F10.5,5(A1,F10.6))
	
	STOP
	END
C THIS IS THE END OF ABLATOR ROUTINE


C **********************************************************
C READ IN MATERIAL PROPERTY DATA
C UNITS INPUT AS CONVENIENT, BUT CONVERT TO SI IN THIS SUBROUTINE
	SUBROUTINE READMAT
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION ACOND(13),AH2T(10),EOSCOEF(10),SIGMACOEF(7),YIELDCOEF(3)
	CHARACTER*50 MATNAME, COMMENT
	
	COMMON/MATDAT/TMELT,HMELTI,HMELTT,ACOND,AH2T
	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF
	COMMON/SURFTEN/SIGMACOEF

	OPEN(UNIT=12,FILE="matdata", STATUS="OLD")
	READ(12,130) MATNAME
	WRITE(*,*) MATNAME
	READ(12,130) COMMENT
C DENSITY IN kg/m**3
	READ(12,*) RHO0
	READ(12,130) COMMENT
C MELT TEMPERATURE IN K
	READ(12,*) TMELT
	READ(12,130) COMMENT
C CURVE FIT COEFFICIENT FOR THERMAL COND VS TEMP
C FIFTH-ORDER POLYNOMIALS FOR SOLID AND LIQUID
C AND ONE PARAMETER FOR VAPOR
C UNITS: cal/m.s.K and K
	DO 4, K=1,6
		READ (12,*) ACOND(K)
4	CONTINUE
	READ(12,130) COMMENT
	DO 5, K=7,12
		READ (12,*) ACOND(K)
5	CONTINUE
	READ(12,130) COMMENT
	READ(12,*) ACOND(13)
C CONVERT TO SI UNITS (W/m.K)
	DO 20, K=1,13
		ACOND(K) = ACOND(K) * 4.184
20	CONTINUE

C CURVE FIT COEFFICIENTS FOR TEMPERATURE VS ENTHALPY
C FIRST 6 FOR SOLID, THEN 2 EACH FOR LIQUID AND VAPOR
C UNITS: K and cal/g
	READ(12,130) COMMENT
	DO 6, K=1,6
		READ(12,*) AH2T(K)
6	CONTINUE
	READ(12,130) COMMENT
	READ(12,*) AH2T(7)
	READ(12,*) AH2T(8)
	READ(12,130) COMMENT
	READ(12,*) AH2T(9)
	READ(12,*) AH2T(10)
C CONVERT TO SI UNITS (J/kg)
	DO 25, K=2,6
		AH2T(K) = AH2T(K) / (4184.**(K-1))
25	CONTINUE
	AH2T(8) = AH2T(8) / 4184.
	AH2T(10) = AH2T(10) / 4184.

C Tboil vs. P COEFFICIENTS
C FROM DUSHMAN: LOG(P) = A - B/T
C UNITS: P in microns, T in K
	READ(12,130) COMMENT
	READ(12,*) ABOIL
	READ(12,*) BBOIL
C CONVERT TO SI UNITS (Pa)
	ABOIL = ABOIL - DLOG10(7.50D+00)

C EQUATION OF STATE COEFFICIENTS
	READ(12,130) COMMENT
	DO 216, K=1,10
		READ(12,*) EOSCOEF(K)
216	CONTINUE

C POISSON'S RATIO
	READ(12,130) COMMENT
	READ(12,*) PRNU

C YIELD STRENGTH
	READ(12,130) COMMENT
	DO K=1,3
		READ(12,*) YIELDCOEF(K)
	END DO

C SURFACE TENSION PARAMETERS
	READ(12,130) COMMENT
	DO 217, K=1,7
		READ(12,*) SIGMACOEF(K)
217	CONTINUE
	
	CLOSE(12)

C COMPUTE ETHALPIES FOR INCIPIENT ANT TOTAL MELT
C START BY ASSUMING A LINEAR FIT FOR LIQUID T VS. H
C UNITS ARE: J/kg and K
	HMELTT = (TMELT - AH2T(7)) / AH2T(8)
C NOW GET INCIPIENT MELT ENERGY VIA NEWTON'S METHOD
	HM = 0.6d+0 * HMELTT
      
	AH2TZERO = AH2T(1) - TMELT
	DO 218, K=1,6
		HM = HM - (AH2TZERO + HM*(AH2T(2) + HM*(AH2T(3) + HM*
     &			(AH2T(4) + HM*(AH2T(5) + HM*AH2T(6)))))) / 
     &			(AH2T(2) + HM*(2.*AH2T(3) + HM*(3.*AH2T(4) + HM*
     &			(4.*AH2T(5) + HM*5.*AH2T(6)))))
218	CONTINUE
	HMELTI = HM
	WRITE(*,*) 'H-MELT-I (CAL/G) =',HM/4184.
	WRITE(*,*) 'H-MELT-T (CAL/G) =',HMELTT/4184.

130	FORMAT(A)

	RETURN
	END

C **********************************************************
C SET ZONE EDGE LOCATIONS, ZONE CENTERS AND ZONE MASSES
C GEOM IS FLAG FOR PLANAR (1), CYLINDRICAL (2) OR SPHERICAL (3)
	SUBROUTINE ZONESET(RHO0,X0,XC,XLOC,ZMASS,GEOM)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	INTEGER GEOM
	PARAMETER(NP1 = 401)
	DIMENSION X0(0:NP1),XC(NP1),XLOC(NP1),ZMASS(NP1)

C PLANAR GEOMETRY
	GEOM = 1
C FIRST ZONE GRID SIZE DX METERS
C	DX = 5E-10
	READ(9,130) COMMENT
	READ(9,*) DX
	DX = DX * 1E-9
	RATIO = 1.02
C THEESE PARAMETERS GIVE TOTAL X = 56 MICRONS FOR 100 ZONES
C ZONE MASS IN kg/m2
C ZONE LOCATIONS IN meters
C INITIAL ZONE CENTER LOCATIONS IN XLOC ARRAY IN microns
	X0(0) = 0.0D+00
	DO 207, I=1,NP1
		X0(I) = X0(0) + DX * (RATIO**I - 1.)/(RATIO-1.)
		CALL DENGEOM(GEOM,X0(I-1),X0(I),RDEN)
		ZMASS(I) = RHO0 * (X0(I) - X0(I-1)) * RDEN
		XC(I) = (X0(I) + X0(I-1)) / 2.
		XLOC(I) = XC(I) * 1.0D+06
207	CONTINUE

130	FORMAT(A)
	RETURN
	END

C **********************************************************
C RETURN GEOMETRY FACTOR TO GET CORRECT ZONE MASSES
	SUBROUTINE DENGEOM(GEOM,R1,R2,RDEN)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	INTEGER GEOM

	PI = 4.0 * ATAN(1.)
	IF (GEOM .EQ. 1) THEN
		RDEN = 1.0D+00
	ELSE IF (GEOM .EQ. 2) THEN
		RDEN = PI * (R1 + R2)
	ELSE 
		RDEN = (4./3.) * PI * (R1**2 + R1*R2 + R2**2)
	END IF
	
	RETURN
	END

C **********************************************************
C RETURN GEOMETRY FACTOR TO GET CORRECT HEAT CONDUCTION
C Q = BETAC * K * DT / DX
C FOR W/m2 planar, W/m cylindrical and W spherical
	SUBROUTINE BETAGEOM(GEOM,R1,R2,BETAC)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	INTEGER GEOM

	PI = 4.0 * ATAN(1.)
	
	IF (GEOM .EQ. 1) THEN
		BETAC = 1.0D+00
	ELSE IF (GEOM .EQ. 2) THEN
		BETAC = PI * (R1 + R2)
	ELSE
		BETAC = PI * (R1 + R2)**2
	END IF

	RETURN
	END

C **********************************************************
C RETURN AREA AT A GIVEN RADIUS DEPENDING ON GEOMETRY
	SUBROUTINE AREAGEOM(GEOM,R,AREA)	
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	INTEGER GEOM

	PI = 4.0 * ATAN(1.)
	
	IF (GEOM .EQ. 1) THEN
		AREA = 1.0D+00
	ELSE IF (GEOM .EQ. 2) THEN
		AREA = 2.0D+00 * PI * R
	ELSE
		AREA = 4.0D+00 * PI * R**2
	END IF

	RETURN
	END

C **********************************************************
C FIND NEW TIME STEP SIZE TO HAVE X0 POINT JUST HIT AXIS
C FOR CYLINDRICAL AND SPHERICAL GEOMETRIES
C ** CAUTION!! UNTESTED ROUTINE **
	SUBROUTINE AXIS(X0,U0,A0,DTOLD,DTNEW)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)

	A = A0 / 2.0
	B = U0 + A0 *DTOLD / 2.0
	C = X0
	B24AC = B**2 - 4*A*C
	IF (B24AC .LT. 0) THEN
		WRITE(*,*) 'ERROR IN AXIS ROUTINE'
		STOP
	END IF
	DTP = (-B + SQRT(B24AC)) / (2.*A)
	DTM = (-B - SQRT(B24AC)) / (2.*A)

C CHOOSE NEW TIME STEP AS SMALLEST POSITIVE ROOT
	IF (DTM .LE. 0.0) THEN
		IF (DTP .GT. 0) THEN
			DTNEW = DTP
		ELSE
			WRITE(*,*) 'ERROR IN AXIS ROUTINE'
			STOP
		END IF
	ELSE
		DTMIN = DTM
	END IF
	
	RETURN
	END

C **********************************************************
C SET UP FOR ENERGY DEPOSITION BASED ON ISOURCE CHOICE
	SUBROUTINE XSOURCE(ISOURCE,LASERFLAG,FBBLEAK,TSTART)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	PARAMETER(NBIN=45)
	DIMENSION BIN(NBIN),BINW(NBIN),FBBLEAK(NBIN)

	COMMON/PULSE/TSQ,ESQ,EGAUSS,SC,FWHM
	COMMON/OPACBINS/BIN,BINW
	
C DEFINE A BLACKBODY FUNCTION
	BBE(JBB,TBB) = BINW(JBB) * ((BIN(JBB)/TBB)**3) / 
     &		(EXP(BIN(JBB)/TBB)-1.)

	IF (ISOURCE .EQ. 1) THEN
C		WRITE(*,*) 'Enter pulse duration (ns)'
C		READ(*,*) TSQ
		READ(9,130) COMMENT
		READ(9,*) TSQ
C		WRITE(*,*) 'Enter total energy in the pulse (J)'
C		READ(*,*) ESQ
		READ(9,130) COMMENT
		READ(9,*) ESQ

		IF (LASERFLAG .NE. 1) THEN
C			WRITE(*,*) 'Enter blackbody temperature (keV)'
C			READ(*,*) BBTLEAK
			READ(9,130) COMMENT
			READ(9,*) BBTLEAK
		ELSE
C SKIP READING BLACKBODY TEMPERATURE FROM THE FILE
			READ(9,130) COMMENT
			READ(9,*) COMMENT
		END IF
		
	ELSE IF (ISOURCE .EQ. 2) THEN
C		WRITE(*,*) 'Enter pulse FWHM (ns)'
C		READ(*,*) FWHM
		READ(9,130) COMMENT
		READ(9,*) FWHM
C		WRITE(*,*) 'Enter total energy in the pulse (J)'
C		READ(*,*) EGAUSS
		READ(9,130) COMMENT
		READ(9,*) EGAUSS
		IF (LASERFLAG .NE. 1) THEN
C			WRITE(*,*) 'Enter blackbody temperature (keV)'
C			READ(*,*) BBTLEAK
			READ(9,130) COMMENT
			READ(9,*) BBTLEAK
		ELSE
C SKIP READING BLACKBODY TEMPERATURE FROM THE FILE
			READ(9,130) COMMENT
			READ(9,130) COMMENT
		END IF
		
		SC = 2. * SQRT(-2. * LOG(0.5)) / FWHM
	ELSE 
		WRITE(*,*) 'Invalid source term number entered'
		STOP
	END IF

	IF (LASERFLAG .NE. 1) THEN
C FRACTION OF BLACKBODY ENERGY IN EACH BIN
C ASSUME SQUARE AND GAUSSIAN PULSES ARE PURE LAMBERTIAN
		SUMLEAK = 0.
		DO J=1,NBIN
			FBBLEAK(J) = BBE(J,BBTLEAK)
			SUMLEAK = SUMLEAK + FBBLEAK(J)
		END DO

C NORMALIZE TO MAKE THESE FRACTIONS OF TOTAL ENERGY
		DO J=1,NBIN
			FBBLEAK(J) = FBBLEAK(J) / SUMLEAK
		END DO
C FOR LASER USE MONOCHROMATIC SPECTRUM
	ELSE
		DO J=1,NBIN
			FBBLEAK(J) = 0.0
		END DO
		FBBLEAK(1) = 1.0
	END IF
C	DO J=1,NBIN
C		WRITE(*,*) FBBLEAK(J)
C	ENDDO

C SET PROBLEM START TIME, TSTART IN ns
	TSTART = 0.0

130	FORMAT(A)
	RETURN
	END

C **********************************************************
C SET MAGNITUDE OF ENERGY PULSES
C RETURNS ENERGY IN EACH BIN FOR THIS TIME STEP
	SUBROUTINE XPULSE(TM,ISOURCE,AREA,FBBLEAK)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	PARAMETER(NBIN=45)
	DIMENSION ELEAK(NBIN),EIN(NBIN),ELEAKOLD(NBIN)
	DIMENSION BIN(NBIN),BINW(NBIN),FBBLEAK(NBIN)

	COMMON/PULSE/TSQ,ESQ,EGAUSS,SC,FWHM
	COMMON/EINTEGRAL/ELEAKOLD,ELEAK,EIN
	COMMON/OPACBINS/BIN,BINW

	EXTERNAL ERF

	TNS = TM * 1.0D+09

	IF (ISOURCE .EQ. 1) THEN
C SQUARE PULSE, STARTS AT TIME = 0
C USE 2% RAMP AT START AND END
		TRAMP = TSQ * 0.02
		IF (TNS .LT. TRAMP) THEN
			FTMLEAK = ESQ * TNS**2 / (2. * TSQ * TRAMP)
		ELSE IF (TNS .LT. TSQ) THEN
			FTMLEAK = ESQ * (TNS-TRAMP/2.) / TSQ
		ELSE IF (TNS .LT. (TSQ+TRAMP)) THEN
			TAU = TNS - TSQ
			FTMLEAK = (ESQ/TSQ)*(TSQ-TRAMP/2.+TAU-TAU**2 / (2.*TRAMP))
		ELSE
			FTMLEAK = ESQ
		END IF

	ELSE
C GAUSSIAN PULSE
		TSTAR = (TNS - 1.5 * FWHM) * SC / SQRT(2.)
		WRITE(*,*) TNS,SC,TSTAR
		A1 = 0.278393
		A2 = 0.230389
		A3 = 0.000972
		A4 = 0.078108
		XIN = TSTAR
		XG = DABS(XIN)
		WRITE(*,*) XG
C		IF (XIN .GE. 0) THEN
			MYERF = DABS(1. - 1. / (1. + XG * (A1 + XG * 
     &				(A2 + XG * (A3 + XG * A4))))**4)
C		ELSE
C			MYERF = -1
C		END IF
		WRITE(*,*) 'MYERF(TSTAR) = ',MYERF,'(',XIN,')'
		FTMLEAK = 0.5 * (0. + MYERF) * EGAUSS
		WRITE(*,*) 'FTMLEAK = ',FTMLEAK
	END IF

C SET UP INCIDENT ENERGY IN EACH BIN IN THIS TIME STEP
C EIN(J) IS IN J/m2; ELEAK ARE IN JOULES
	DO 33, J=1,NBIN
		ELEAK(J) = FBBLEAK(J) * FTMLEAK
		ELNET = ELEAK(J) - ELEAKOLD(J)
		EIN(J) = ELNET / AREA
		ELEAKOLD(J) = ELEAK(J)
C		WRITE(*,*) J,EIN(J),FTMLEAK
33	CONTINUE

	RETURN
	END

C **********************************************************
C DEFINE THERMAL CONDUCTIVITY VS TEMPERATURE
C USE UP TO A FIFTH-ORDER POLYNOMIAL
C UNITS ARE: W/m.K and K IN CURVE FITS
	SUBROUTINE CONDVST(T,VOLF,COND)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION AS(0:5), AL(0:5), AH2T(10)
	COMMON/MATDAT/TMELT,HMELTI,HMELTT,AS,AL,AV,AH2T

	IF (T .LT. 0.0) THEN
		WRITE(*,*) 'T =',T
		STOP
	END IF
	
	CONDS = AS(0) + T*(AS(1) + T*(AS(2) + T*(AS(3) + 
     &		T*(AS(4) + T*AS(5)))))
	CONDL = AL(0) + T*(AL(1) + T*(AL(2) + T*(AL(3) + 
     &		T*(AL(4) + T*AL(5)))))

C MONOATOMIC GAS CONDUCTIVITY
	CONDV = AV * SQRT(T)

	IF (VOLF .LE. 1.0D-01) THEN
		IF (T .LT. (TMELT-.1)) THEN
			COND = CONDS
		ELSE IF (T .GT. (TMELT+.1)) THEN
			COND = CONDL
		ELSE
C TWO PHASE LIQUID/SOLID REQION
			COND = 2. * CONDS * CONDL / (CONDS + CONDL)
		END IF
	ELSE IF (VOLF .GE. 0.99) THEN
		COND = CONDV
	ELSE
C TWO PHASE LIQUID/VAPOR REGION
		COND = CONDV
	END IF

	RETURN
	END

C **********************************************************
C CONVERT ENTHALPY TO A TEMPERATURE
C FOR BOTH SOLID AND LIQUID
C USE UP TO A FIFTH-ORDER POLYNOMIALS
C UNITS ARE: J/kg and K IN CURVE FITS
	SUBROUTINE H2T(HJKG,T)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION ACOND(13), AS(0:5)
	COMMON/MATDAT/TMELT,HMELTI,HMELTT,ACOND,AS,AL0,AL1,AV0,AV1

	H = HJKG
	TS = AS(0) + H*(AS(1) + H*(AS(2) + H*(AS(3) + 
     &		H*(AS(4) + H*AS(5)))))
	TL = AL0 + H*AL1

	IF (TL .GT. TMELT) THEN
		T = TL
	ELSE IF (TS .LT. TMELT) THEN
		T = TS
	ELSE 
		T = TMELT
	END IF

	RETURN
	END

C **********************************************************
C DETERMINE HEAT CAPACITY (Cv) FOR SOLID, LIQUID AND VAPOR
C VAPOR IS BASED ON IDEAL GAS, USSING GAMMA & RBAR
C LIQUID AND SOLID USE T vs. ENTHALPY RELATIONS(Cp~Cv)
C DECIDE STATE BASED ON VOLUME FRACTION VAPOR (VOLF)
C USE UP TO A FIFTH-ORDER POLYNOMIALS
C UNITS ARE: J/kg and K IN CURVE FITS
	SUBROUTINE HEATCAP(VOLF,T,HG,CV)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION ACOND(13), AS(0:5), EOSCOEF(10)
	DIMENSION YIELDCOEF(3)
	COMMON/MATDAT/TMELT,HMELTI,HMELTT,ACOND,AS,AL0,AL1,AV0,AV1
	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF
	
	GAMMA = EOSCOEF(9)
	RBAR = EOSCOEF(10)

	CVVAPOR = RBAR / (GAMMA - 1.0D+00)
	CVLIQUID = 1.0D+00 / AL1
	CVSOLID = 1.0D+00 / (AS(1) + HG*(2.*AS(2) + 
     &		HG*(3.*AS(3) + HG*(4.*AS(4) + HG*5.*AS(5)))))
	
	IF (VOLF .GT. 0.999999) THEN
C VAPOR
		CV = CVVAPOR
	ELSE IF (VOLF .LT. 0.001) THEN
		IF (T .GT. TMELT) THEN
C LIQIUD
			CV = CVLIQUID
		ELSE IF (T .LT. TMELT) THEN
C SOLID
			CV = CVSOLID
		ELSE
C TWO-PHASE SOLID/LIQUID
			CV = MIN(CVLIQUID,CVSOLID)
		END IF
	ELSE
C TWO-PHASE VAPOR/LIQUID
		CV = MIN(CVLIQUID,CVVAPOR)
	END IF

	RETURN
	END


C **********************************************************
C APPROXIMATION TO ERF(X) FROM ABRAMOWITZ & STEGUN
	DOUBLE PRECISION FUNCTION ERF(XIN)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	
	A1 = 0.278393
	A2 = 0.230389
	A3 = 0.000972
	A4 = 0.078108

	X = DABS(XIN)
	IF (XIN .GE. 0) THEN
		ERF = ABS(1. - 1. / (1. + X * (A1 + X * 
     &			(A2 + X * (A3 + X * A4))))**4)
	ELSE
		ERF = -1
	END IF

	RETURN
	END

C **********************************************************
C COMPUTE EQUATION OF STATE COEFFICIENTS
C F1 = F1(RHO) AND F2 = F2(RHO)
C FOR LINEAR EOS: PRESSURE = F1 + F2 * ENERGY
	SUBROUTINE EOSF(RHO,F1,F2)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION EOSCOEF(10), C(0:6)
	DIMENSION YIELDCOEF(3)
	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF

	U = RHO/RHO0 - 1.0D+00
C EOS TYPE
	ITYPE = NINT(EOSCOEF(1))

	IF (ITYPE .EQ. 1) THEN
C STIENBERG'S MODEL (DYNA2D EOS #4)
		C0 = EOSCOEF(2)
		S1 = EOSCOEF(3)
		S2 = EOSCOEF(4)
		S3 = EOSCOEF(5)
		G0 = EOSCOEF(6)
		B = EOSCOEF(7)
		IF (U .LE. 0.) THEN
			F1 = RHO0 * C0**2 * U
		ELSE
			F1 = RHO0 * C0**2 * U * 
     &				(1. + (1. - G0/2.) * U + B/2. * U**2) / 
     &				(1. - (S1-1.)*U - S2*U**2/(U+1.) - S3*U**3/(U+1.)**2)
		END IF
		F2 = (G0 + B * U) * RHO
	ELSE IF (ITYPE .EQ. 2) THEN
C LINEAR POLYNOMIAL MODEL (DYNA2D EOS #1 AND #6)
		DO 20, K=0,6
			C(K) = EOSCOEF(K+2)
20		CONTINUE
		IF (U .LE. 0.) THEN
			F1 = C(0) + C(1)*U + C(3)*U**3
			F2 = (C(4) + C(5)*U) * RHO
		ELSE
			F1 = C(0) + C(1)*U + C(2)*U**2 + C(3)*U**3
			F2 = (C(4) + C(5)*U + C(6)*U**2) * RHO
		END IF
	ELSE 
		WRITE(*,*) 'NOT A VALID EOS NUMBER'
		STOP
	END IF

	RETURN
	END

C **********************************************************
C COMPUTE EQUATION OF STATE COEFFICIENT DERIVATIVES
C F1 = F1(RHO) AND F2 = F2(RHO)
C FOR LINEAR EOS: PRESSURE = F1 + F2 * ENERGY
	SUBROUTINE EOSDF(RHO,DF1,DF2)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION EOSCOEF(10), C(0:6),YIELDCOEF(3)
	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF

	U = RHO/RHO0 - 1.0D+00
C EOS TYPE
	ITYPE = NINT(EOSCOEF(1))

	IF (ITYPE .EQ. 1) THEN
C STIENBERG'S MODEL (DYNA2D EOS #4)
		C0 = EOSCOEF(2)
		S1 = EOSCOEF(3)
		S2 = EOSCOEF(4)
		S3 = EOSCOEF(5)
		G0 = EOSCOEF(6)
		B = EOSCOEF(7)
		IF (U .LE. 0.) THEN
			DF1 = C0**2
		ELSE
			XNUMER = C0**2*RHO*(2*RHO**3 + 4.*(1.-G0/2.)*RHO**3*U + 
     &				3.*B*RHO**3*U**2 - 2.*(1.-G0/2.)*(S1-1.)*RHO**3*U**2 + 
     &				2.*RHO**2*RHO0*S2*U**2 - 2.*B*(S1-1.)*RHO**3*U**3 - 
     &				2.*RHO*RHO0**2*S2*U**3 + 4.*RHO*RHO0**2*S3*U**3 - 
     &				B*RHO**2*RHO0*S2*U**4 - 
     &				2.*(1-G0/2.)*RHO*RHO0**2*S2*U**4 + 
     &				2.*(1-G0/2.)*RHO*RHO0**2*S3*U**4 - 
     &				4.*RHO0**3*S3*U**4 - B*RHO*RHO0**2*S2*U**5 - 
     &				4.*(1.-G0/2.)*RHO0**3*S3*U**5 - 2.*B*RHO0**3*S3*U**6)
			XDENOM = 2.*(-RHO**2 + (S1-1.)*RHO**2*U + 
     &				RHO*RHO0*S2*U**2 + RHO0**2*S3*U**3)**2
			DF1 = XNUMER / XDENOM
		END IF
		DF2 = G0 - B + 2. * B * RHO / RHO0

	ELSE IF (ITYPE .EQ. 2) THEN
C LINEAR POLYNOMIAL MODEL (DYNA2D EOS #1 AND #6)
		DO 20, K=0,6
			C(K) = EOSCOEF(K+2)
20		CONTINUE
		IF (U .LE. 0.) THEN
			DF1 = (C(1) + 3.*C(3)*U**2) / RHO0
			DF2 = C(4) - C(5) + 2. * C(5) * RHO / RHO0
		ELSE
			DF1 = (C(1) + 2.*C(2)*U + 3.*C(3)*U**2) / RHO0
			DF2 = C(4) - C(5) + 2.*C(5)*RHO/RHO0 + 
     &				C(6)*(1.D+00+RHO/RHO0*(-4.D+00+3.*RHO/RHO0))
		END IF
	ELSE 
		WRITE(*,*) 'NOT A VALID EOS NUMBER'
		STOP
	END IF

	RETURN
	END

C **********************************************************
C COMPUTE EQUATION OF STATE COEFFICIENTS FOR PURE VAPOR
C F1 = F1(RHO) AND F2 = F2(RHO)
C FOR LINEAR EOS: PRESSURE = F1 + F2 * ENERGY
C ASSUMES IDEAL GAS EOS: P = RHO * RBAR * T
	SUBROUTINE FVAPOR(RHO,F1,F2)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION ACOND(13),AH2T(10),EOSCOEF(10)
	DIMENSION YIELDCOEF(3)
	COMMON/MATDAT/TMELT,HMELTI,HMELTT,ACOND,AH2T
	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF

	AV0 = AH2T(9)
	AV1 = AH2T(10)
	GAMMA = EOSCOEF(9)

	F1 = 0.0
	F2 = (GAMMA - 1.0D+00) * RHO

	RETURN
	END

C **********************************************************
C COMPUTE SOUND SPEED IN SOLID/LIQUID
C USING DERIVATIVES OF EQUATION OF STATE COEFFICIENTS
C F1 = F1(RHO) AND F2 = F2(RHO)
C FOR LINEAR EOS: PRESSURE = F1 + F2 * ENERGY
	SUBROUTINE SOUND(HG,RHO,P,F2,C)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION EOSCOEF(10),YIELDCOEF(3)
	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF
	
	CALL EOSDF(RHO,DF1,DF2)
	
	C = SQRT(DF1 + HG*DF2 + P*F2/RHO**2)

	RETURN
	END

C **********************************************************
C EQUATION OF STATE COEFFICIENT FOR SPALLED MATERIAL (LIQUID AND/OR VAPOR)
C P = F1(RHO) + F2(RHO) * HG
C FOR LIQUID PART, USE SOLID (UNSPALLED) EOS
C FOR VAPOR PART, USE IDEAL GAS RELATION
C BASE TWO-PHASE FITTING ON Tsat vs. Psat CURVE
C UNITS: AL,AV COEFFICIENTS IN J/kg, PRESSURE IN Pa
	SUBROUTINE F2PHASE(RHO,HG,QUAL,F1,F2)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)

C CALL EOS ROUTINE TWICE WITH SLIGHTLY DIFFERENT ENERGIES
	HG1 = HG
	DELTAHG = HG * 0.01
	HG2 = HG + DELTAHG
	CALL EOSPALL(HG1,RHO,T,P1,QUAL,VOLF,CSOUND)
	CALL EOSPALL(HG2,RHO,T,P2,QUAL,VOLF,CSOUND)
	F2 = (P2-P1) / DELTAHG
	F1 = P1 - F2 * HG1

	RETURN
	END

C **********************************************************
C EQUATION OF STATE FOR SPALLED MATERIAL (LIQUID AND/OR VAPOR)
C ALSO COMPUTE SOUND SPEED
C FOR LIQUID PART, USE SOLID (UNSPALLED) EOS
C FOR VAPOR PART, USE IDEAL GAS RELATION
C BASE TWO-PHASE FITTING ON Tsat vs. Psat CURVE
C NOTE THAT QUAL (OLD VALUE) IS SENT AS AN INITIAL GUESS
C UNITS: AL,AV COEFFICIENTS IN J/kg, PRESSURE IN Pa
	SUBROUTINE EOSPALL(HG,RHO,T,P,QUAL,VOLF,CSOUND)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION ACOND(13),AH2T(10),EOSCOEF(10)
	DIMENSION YIELDCOEF(3)

	EXTERNAL QTWO
	
	COMMON/MATDAT/TMELT,HMELTI,HMELTT,ACOND,AH2T
	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF
	COMMON/QTWODAT/HGTWO,RHOTWO,RHOV,RHOL,TTWO,HGL

C MINIMUM PRESSURE IN CHAMBER IS 10^-6 TORR
	PMIN = 1.33D-04
	GAMMA = EOSCOEF(9)
	RBAR = EOSCOEF(10)
	
	TINF = AH2T(1)
	AL0 = AH2T(7)
	AL1 = AH2T(8)
	AV0 = AH2T(9)
	AV1 = AH2T(10)

C FIRST SEE IF LIQUID STATE WORKS OUT FOR LOW-QUALITY ZONE
	TL = AL0 + HG * AL1
	CALL EOSF(RHO,F1,F2)
	PL = F1 + F2 * HG
	PSAT = 10.**(ABOIL-BBOIL/TL)
	IF (PL .GT. PSAT .AND. QUAL .LT. 0.05) THEN
C PURE LIQUID
		P = PL
		T = TL
		CALL SOUND(HG,RHO,PL,F2,CSOUND)
		QUAL = 0.0
		VOLF = 0.0
	ELSE

C SEE IF PURE VAPOR WORKS FOR HIGH-QUALITY ZONE
		TV = AV0 + HG * AV1
		PV = RHO * RBAR * TV
		PSAT = 10.**(ABOIL-BBOIL/TL)
		IF (PV .LT. PSAT .AND. QUAL .GT. 0.95) THEN
C PURE VAPOR WORKS
			T = TV
			P = PV
			CSOUND = SQRT(RBAR*GAMMA*T)
			QUAL = 1.0
			VOLF = 1.0
		ELSE

C TWO-PHASE REGION
C SEE IF TWO-PHASE BASED ON CURRENT QUALITY WILL WORK
			HGTWO = HG
			RHOTWO = RHO
			QLOW = MAX(0.0,QUAL-0.05)
			QHIGH = MIN(1.0,QUAL+0.05)
			TESTLOW = QTWO(QLOW)
			TESTHIGH = QTWO(QHIGH)
			TESTPROD = TESTLOW * TESTHIGH
			IF (TESTPROD .LT. 0.0) THEN

C EQUILIBRUM TWO-PHASE REGION
C ITERATE ON QUALITY USING BRENT'S ROUTINE
				TOL = 1.0D-08
				QUAL = ZBRENT(QTWO,QLOW,QHIGH,TOL)
			ELSE

C TWO-PAHES BRACKETING FAILED, BASED ON LATEST QUALITY
C NON-EQUILIBRUM REGION
				IF (ABS(TESTHIGH) .LT. ABS(TESTLOW)) THEN
					QUAL = QHIGH
				ELSE IF (ABS(TESTHIGH) .GT. ABS(TESTLOW)) THEN
					QUAL = QLOW
				ELSE 
					QUAL = QUAL
				END IF
C USE TTWO AND HGL FROM QTWO
				TEST = QTWO(QUAL)
				PSAT = 10.**(ABOIL-BBOIL/TTWO)
C GET RHOL FROM ITERATIONS OF NONLINEAR FUNCTIONS F1,F2
				RHOL = RHO0
				CALL NEWTP(HGL,PSAT,RHOL)
C DETERMINE NON-EQUILIBRUM VAPOR DENSITY
				IF (QUAL .GT. 0.0) THEN
					RHOV = RHOL / (1.0D+00-(1.0D+00-(RHOL/RHO))/QUAL)
				ELSE
					RHOV = PSAT / (RBAR * TTWO)
				END IF
			END IF

C COMPUTE THE REST OF THE TWO-PHASE PROPERTIES
			T = TTWO
C ZONE PRESSURE WILL BE SATURATION PRESSURE
			P = (10.**(ABOIL - BBOIL/T))
			VOLF = QUAL * RHO / RHOV
			IF (QUAL .GT. 0.999) THEN
				VOLF = MIN(VOLF,1.0D+00)
			END IF
			IF (VOLF .GE. 0.0D+00 .AND. VOLF .LE. 1.0D+00) THEN
C NOW COMPUTE SOUND SPEDD
				CALL EOSF(RHOL,F1,F2)
				CALL SOUND(HGL,RHOL,P,F2,CLIQ)
				CVAP = SQRT(RBAR*GAMMA*T)
C VOLUME FRACTION OF VAPOR FOR CRUDE INITIAL GUESS AT SOUND SPEED
				CSOUND = VOLF*CVAP + (1.-VOLF)*CLIQ
			ELSE
C THERE IS A PROBLEM WITH VOLF
				WRITE(*,*) 'PROBLEM WITH VOLF IN EOSTWO'
				WRITE(*,*) 'VOLF =',VOLF
				WRITE(*,*) 'QUAL =',QUAL
				WRITE(*,*) 'RHO =',RHO
				WRITE(*,*) 'RHOV =',RHOV
				STOP
			END IF
		END IF
	END IF
	
	RETURN
	END

C **********************************************************
C EQUATION OF STATE FOR 2-PHASE SPALLED MATERIAL (LIQUID AND VAPOR)
C ALSO COMPUTE SOUND SPEED
C FOR LIQUID PART, USE CURRENT LIQUID/SOLID EOS
C FOR VAPOR PART, USE IDEAL GAS RELATION
C BASE TWO-PHASE FITTING ON Tsat vs. Psat CURVE
C UNITS: AL,AV COEFFICIENTS IN J/kg, PRESSURE IN Pa
	FUNCTION QTWO(QUAL)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION ACOND(13),AH2T(10),EOSCOEF(10),YIELDCOEF(3)

	COMMON/MATDAT/TMELT,HMELTI,HMELTT,ACOND,AH2T
	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF
	COMMON/QTWODAT/HG,RHO,RHOV,RHOL,T,HGL

	AL0 = AH2T(7)
	AL1 = AH2T(8)
	AV0 = AH2T(9)
	AV1 = AH2T(10)
	GAMMA = EOSCOEF(9)
	RBAR = EOSCOEF(10)
	
C DETERMINE TEMPERATURE BASED ON ENERGY BALANCE
	Z1 = HG + QUAL*AV0/AV1 + (1.0D+00-QUAL)*AL0/AL1
	Z2 = (QUAL/AV1) + ((1.0D+00-QUAL)/AL1)
	T = Z1 / Z2
C ENTHALPIES FOLLOW DIRECTLY
	HGV = (T-AV0) / AV1
	HGL = (T-AL0) / AL1
C ZONE PRESSURE WILL BE SATURATION PRESSURE
	PSAT = (10.**(ABOIL - BBOIL/T))
C VAPOR DENSITY FROM IDEAL GAS RELATION
	RHOV = PSAT / (RBAR * T)
C GET RHOL FROM ITERATIONS ON NONLINEAR FUNCTIONS F1,F2
	RHOL = RHO0
	CALL NEWTP(HGL,PSAT,RHOL)

C GET QUALITY IMPLIED BY MASS BALANCE
	QUALRHO = (1.0D+00-RHOL/RHO) / (1.0D+00-RHOL/RHOV)

C QUALITIES MUST BE EQUAL IN CONVERGED SOLUTION
	QTWO = QUAL - QUALRHO

	RETURN
	END

C **********************************************************
C NEWTON'S METHOD ITERATION TO FIND DENSITY,
C GIVEN PRESSURE AND SPECIFIC ENERGY
C F1 = F1(RHO) AND F2 = F2(RHO)
C FOR LINEAR EOS: PRESSURE = F1 + F2 * ENERGY
	SUBROUTINE NEWTP(HG,P,RHO)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)

	TOL = 1.0D-06
	K = 0
	RHONEW = RHO

10	CONTINUE

	RHO = RHONEW
	CALL EOSF(RHO,F1,F2)
	CALL EOSDF(RHO,DF1,DF2)
	F = F1 + F2*HG - P
	FP = DF1 + HG*DF2
	RHONEW = RHO - F/FP
	
	K = K+1
	IF (K .GT. 10) THEN
		WRITE(*,*) 'TOO MANY ITERATIONS ON RHOL'
		STOP
	END IF

	IF (ABS(RHONEW-RHO) .GT. TOL) GOTO 10
	RHO = RHONEW

	RETURN
	END

C **********************************************************
C CALCULATION OF SURFACE EVAPORATION MASS AND ENERGY LOSS
C FOR CRUDE ESTIMATE USING MAXIMUM FLUX (PAMB=0)
C LIMIT FROM KELLEY & ROTHENBERG, NUC INST & METH, 1985, p 755
	SUBROUTINE SURFVAP(GEOM,PAMB,TSAT,X,DT,DVAPMASS,DHVAP)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	INTEGER GEOM
	DIMENSION ACOND(13),AH2T(10),EOSCOEF(10)
	DIMENSION YIELDCOEF(3)

	COMMON/MATDAT/TMELT,HMELTI,HMELTT,ACOND,AH2T
	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF

	PI = 4.0 * ATAN(1.0)
	RBAR = EOSCOEF(10)

C VAPOR PRESSURE FUNCTION FROM DUSHMAN, PRESSURE IN Pa
	PSAT = (10.**(ABOIL - BBOIL/TSAT))
C FLUX IN kg/m2.sec
	FLUX = PSAT / SQRT(2. * PI * RBAR * TSAT)

	IF (GEOM .EQ. 1) THEN
		AREA = 1.0D+00
	ELSE IF (GEOM .EQ. 2) THEN
		AREA = 2.0 * PI * X
	ELSE
		AREA = 4.0 * PI * X**2
	END IF
	DVAPMASS = FLUX * DT * AREA

C ENERGY IN THIS MASS
C ASSUME VAPOR AT CURRENT ZONE TEMPERATURE
C USES LINEAR FIT TO T VS. H CURVE (in K vs. J/kg)
	HG = (TSAT - AH2T(9)) / AH2T(10)
	HL = (TSAT - AH2T(7)) / AH2T(8)
	DHVAP = (HG - HL) * DVAPMASS

	RETURN
	END

C **********************************************************
C COMPUTE EQUATION OF STATE COEFFICIENTS FOR SURFACE ZONE
C F1 = F1(RHO) AND F2 = F2(RHO)
C FOR LINEAR EOS: PRESSURE = F1 + F2 * ENERGY
C ASSUMES IDEAL GAS EOS: P = (GAMMA-1)*RHO*HG
C OR JUST USE SOLID EOS FOR LOW VAPOR FRACTION
	SUBROUTINE FSURF(RHO,RHOL,QUAL,QMIN,F1,F2)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	
	IF (QUAL .LE. QMIN) THEN
C JUST LIQUID/SOLID IN ZONE, SO IGNORE VAPOR
		CALL EOSF(RHO,F1,F2)
	ELSE
C APPROXIMATION BASED ON CONSTANT RHOL FROM LAST TIME STEP
		RHOV = QUAL / ((1./RHO) + (QUAL - 1.0D+00)/RHOL)
		CALL FVAPOR(RHOV,F1,F2)
	END IF

	RETURN
	END

C **********************************************************
C ROUTINE TO DETERMINE NEW VELOCITY OF OUTER NODE
C BASED ON PSEUDO-STEADY ISENTROPIC IDEAL GAS FLOW REALTIONS
C ASSUME VAPORIZING SURFACE IS STATIONARY
C VAPORIZING SURFACE AT P0=Psat, OUTER SURFACE AT P=P(0)
	SUBROUTINE USURFIX(T,P0,P,UNEW)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION EOSCOEF(10),YIELDCOEF(3)

	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF

	GAMMA = EOSCOEF(9)
	RBAR = EOSCOEF(10)
	GM1 = GAMMA - 1.0D+00

	C0 = SQRT(RBAR*GAMMA*T)

	UNEW = C0*SQRT((2./GM1)*DABS((1.0D+00-(P/P0)**(GM1/GAMMA))))

C KEEP WITH SIGN CONVENTION
	IF (P .LT. P0) THEN
		UNEW = -UNEW
	END IF
	
	RETURN
	END

C **********************************************************
C ROUTINE TO DETERMINE NEW PRESSURE AT OUTER NODE OF ISURF
C BASED ON PSEUDO-STEADY ISENTROPIC IDEAL GAS FLOW REALTIONS
C C/C0 = 1 - B*X LINEAR ACCROS EXPANSION FAN
C BUT ASYMPTOTE TO IDEAL GASS PRESSURE IN SPALLED ZONE
	SUBROUTINE PLEFTCALC(PSAT,PC,DXC1,DX1,RHOV,QUAL,T,PLEFT)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION EOSCOEF(10),YIELDCOEF(3)

	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF

	GAMMA = EOSCOEF(9)
	RBAR = EOSCOEF(10)
	GM1 = GAMMA - 1.0D+00
	N = 4

C ASSUME AVERAGE DROP ACROSS SONIC KNUDSEN LAYER (KNIGHT)
C P0 = 0.235 * PSAT
C ASSUME NO DROP ACROSS KNUDSEN LAYER
	P0 = PSAT
	B = (1.0D+00-(PC/P0)**(GM1/(2.*GAMMA)))/DXC1
	PEXP = P0 * (1.0D+00 - B*DX1)**(2.*GAMMA/GM1)
	PEXP = MAX(PEXP,PC)

C AVERAGE VAPOR PRESSURE
	PVAP = RHOV * RBAR * T

C INTERPOLATE BETWEEN THESE PRESSURES WITH Nth ORDER FUNCTION
	QSTAR = (1.0D+00 - QUAL)**N
	PLEFT = PEXP * QSTAR + (1.0D+00 - QSTAR) * PVAP

	RETURN
	END

C **********************************************************
C EQUATION OF STATE FOR (IN GENERAL) 2-PHASE SURFACE ZONE
C FOR LIQUID PART, USE CURRENT LIQUID/SOLID EOS
C FOR VAPOR PART, USE IDEAL GAS RELATION
C UNITS: AL,AV COEFFICIENTS IN J/kg, PRESSURE IN Pa
C NOTE THAT T PROVIDES BOTH AN INITIAL TEMPERATURE GUESS
C  AND RETURNS THE FINAL TEMPERATURE RESULT
	SUBROUTINE EOSURF(RHOL,RHOV,RHOP1,QMIN,PM1,P,ALPHA,CSOUND)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION ACOND(13),AH2T(10),EOSCOEF(10)
	DIMENSION YIELDCOEF(3)

	EXTERNAL TSURF, PSURF
	
	COMMON/MATDAT/TMELT,HMELTI,HMELTT,ACOND,AH2T
	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF
	COMMON/SURFT/HGSURF,QUALSURFT
	COMMON/SURFP/RHOSURF,QUALSURFP,HGV,HGL,T

	AL0 = AH2T(7)
	AL1 = AH2T(8)
	AV0 = AH2T(9)
	AV1 = AH2T(10)
	GAMMA = EOSCOEF(9)
	RBAR = EOSCOEF(10)
	QUAL = QUALSURFT
	HG = HGSURF
	RHO = RHOSURF

C ITERATE ON TEMPERATURE CHOICE TO MATCH VALUES OBTAINED FOR LIQUID AND VAPOR
C SOLVE ISING BRENT'S METHOD (SEE NUMERICAL RECIPES)
C HG1 AND HG2 MUST BRACKET THE ROOT TO Tvapor-Tliquid=0,
C WHICH IS IN THE EXTERNAL FUNCTION TSURF(HGV)
C WHERE HGV IS THE SPECIFIC ENERGY OF THE VAPOR PHASE
C FIRST GET TWO HGV VALUES THAT BRACKET FUNCTION ZERO
	HGNOM = (T - AV0) / AV1
	CALL BRACKET(TSURF,HGNOM,HG1,HG2)
	TOL = 1.0D-05
	HGV = ZBRENT(TSURF,HG1,HG2,TOL)
	HGL = (HG - QUAL*HGV) / (1.0D+00 - QUAL)

	TV = AV0 + AV1 * HGV
	CALL H2T(HGL,TL)

	TRATIO = 2.*ABS(TV-TL)/(TV+TL)
	IF (TRATIO .GT. 0.01) THEN
		WRITE(*,*) 'PROBLEM IN ISURF TEMP MATCH'
		WRITE(*,*) 'TL, TV =', TL,TV
		STOP
	ELSE
		T = (TL + TV) / 2.00D+00
	END IF

C NOW DETERMINE PRESSURE IN THE SURFACE ZONE
C FOR SMALL VAPOR CONTENT, ASSUME FULLY LIQUID/SOLID AS IN FSURF
C OTHERWISE, FIX PRESSURE AT Psat
	IF (QUAL .LE. QMIN) THEN
		RHOL = RHO
		CALL EOSF(RHO,F1,F2)
		P = F1 + F2 * HG
		CALL SOUND(HG,RHO,P,F2,CSOUND)
		ALPHA = 0.0D+00
		RHOV = P / (RBAR * T)
	ELSE

C SET PRESSURE EQUAL TO THE SATURATION VAPOR PRESSURE
C OR THE PRESSURE IN THE NEXT VAPOR ZONE, WHICHEVER IS HIGHER
		PSAT = (10.**(ABOIL - BBOIL/T))
		P = MAX(PSAT,PM1)

C UPDATE LIQUID DENSITY BASED ON THIS PRESSURE
C MUST ITERATE WITH BRENT'S METHOD
C FIRST GET TWO RHOL VALUES THAT BRACKET FUNCTION ZERO
		RNOM = RHOL
		CALL BRACKET(PSURF,RNOM,RL1,RL2)
		TOL = 1.0D-05
		RHOL = ZBRENT(PSURF,RL1,RL2,TOL)
C UNDER SOME CONDITIONS
C  RHOL MAY CALCULATE TO BE LESS THAN RHO
C  SET A FLOOR DENSITY ON WHICH TO BASE A RHOV CALCULATION
C  ADJUST ZONE PRESSURE ACCORDINGLY
		IF (RHOL .LT. RHO) THEN
			IF (RHOP1 .GT. RHO) THEN
				RHOL = RHOP1
			ELSE
				RHOL = RHO + 1.0
			END IF
			CALL EOSF(RHOL,F1,F2)
			P = F1 + F2 * HGL
		END IF

C COMPUTE VOLUME FRACTION VAPOR
		RHOV = QUAL / ((1./RHO) + (QUAL - 1.0D+00)/RHOL)
		ALPHA = QUAL * RHO / RHOV
C NOW COMPUTE SOUND SPEED
		CALL EOSF(RHOL,F1,F2)
		CALL SOUND(HGL,RHOL,P,F2,CLIQ)
		CVAP = SQRT(RBAR*GAMMA*T)
C VOLUME FRACTION OF VAPOR FOR CRUDE GUES AT SOUND SPEED
		CSOUND = ALPHA*CVAP + (1.-ALPHA)*CLIQ
	END IF

	RETURN
	END

C **********************************************************
C TEMPERATURE ITERATION FOR 2-PHASE SURFACE ZONE
C NOTE THAT T PROVIDES BOTH AN INITIAL TEMPERATURE GUESS
C  AND RETURNS THE FINAL TEMPERATURE RESULT
	SUBROUTINE EOSURFT(QMIN,T)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION ACOND(13),AH2T(10),EOSCOEF(10)
	DIMENSION YIELDCOEF(3)

	EXTERNAL TSURF
	
	COMMON/MATDAT/TMELT,HMELTI,HMELTT,ACOND,AH2T
	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF
	COMMON/SURFT/HG,QUAL

	AL0 = AH2T(7)
	AL1 = AH2T(8)
	AV0 = AH2T(9)
	AV1 = AH2T(10)
	GAMMA = EOSCOEF(9)
	RBAR = EOSCOEF(10)

	IF (QUAL .LT. QMIN) THEN
C ALL LIQUID OR SOLID
		CALL H2T(HG,T)
	ELSE IF (QUAL .GT. 0.99999) THEN
C ESSENTIALLY ALL VAPOR
		T = AV0 + AV1 * HG
	ELSE
C ITERATE ON TEMPERATURE CHOICE TO MATCH VALUES OBTAINED FOR LIQUID AND VAPOR
C SOLVE ISING BRENT'S METHOD (SEE NUMERICAL RECIPES)
C HG1 AND HG2 MUST BRACKET THE ROOT TO Tvapor-Tliquid=0,
C WHICH IS IN THE EXTERNAL FUNCTION TSURF(HGV)
C WHERE HGV IS THE SPECIFIC ENERGY OF THE VAPOR PHASE
C FIRST GET TWO HGV VALUES THAT BRACKET FUNCTION ZERO
		HGNOM = (T - AV0) / AV1
		CALL BRACKET(TSURF,HGNOM,HG1,HG2)
		TOL = 1.0D-05
		HGV = ZBRENT(TSURF,HG1,HG2,TOL)
		HGL = (HG - QUAL*HGV) / (1.0D+00 - QUAL)
		TV = AV0 + AV1 * HGV
		CALL H2T(HGL,TL)
		TRATIO = 2.*ABS(TV-TL)/(TV+TL)
		IF (TRATIO .GT. 0.01) THEN
			WRITE(*,*) 'PROBLEM IN ISURF TEMP MATCH'
			WRITE(*,*) 'TL, TV =', TL,TV
			STOP
		ELSE
			T = (TL + TV) / 2.00D+00
		END IF
	END IF

	RETURN
	END

C **********************************************************
C TEMP MATCHING FUNCTION FOR (IN GENERAL) 2-PHASE SURFACE ZONE
C FORM IS TSURF = Tvap - Tliq -> 0 WITH BRENT'S METHOD
C TSURF IS A FUNCTION OF VAPOR ENERGY (HGV)
C ROOT SOLVER WILL VARY ENERGY PARTITION VAPOR/LIQUID TO MATCH TEMPS
	FUNCTION TSURF(HGV)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION ACOND(13),AH2T(10)

	COMMON/MATDAT/TMELT,HMELTI,HMELTT,ACOND,AH2T
	COMMON/SURFT/HG,QUAL

	AV0 = AH2T(9)
	AV1 = AH2T(10)

	HGL = (HG - QUAL*HGV) / (1.0D+00 - QUAL)
	TV = AV0 + AV1 * HGV
	CALL H2T(HGL,TL)
	TSURF = TV - TL

	RETURN
	END

C **********************************************************
C PRESSURE MATCHING FUNCTION FOR 2-PHASE SURFACE ZONE
C FORM IS PSURF = Psat - Tliq -> 0 WITH BRENT'S METHOD
C PSURF IS A FUNCTION OF LIQUID DENSITY (RHOL)
C ROOT SOLVER WILL VARY LIQUID DENSITY TO MATCH PRESSURES
	FUNCTION PSURF(RHOL)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DIMENSION EOSCOEF(10),YIELDCOEF(3)
	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF
	COMMON/SURFP/RHO,QUAL,HGV,HGL,T

	PSAT = (10.**(ABOIL - BBOIL/T))

	CALL EOSF(RHOL,F1L,F2L)
	PL = F1L + F2L * HGL

	PSURF = PSAT - PL

	RETURN
	END

C **********************************************************
C BRENT'S METHOD TO FIND THE ROOT OF A FUNCTION FUNC KNOWN TO
C LIE BETWEEN X1 AND X2. THE RROT, ZBRENT, WILL BE REFINED UNTIL
C ITS ACCURACY IS TOL
C PARAMETERS: MAX # OF ITERATIONS, MACHINE FLOATING-POINT PRECISION
C REFERENCE: NUMERICAL RECIPES, SECTION 9.3
C IS RECURSIVE
	FUNCTION ZBRENT(FUNC,X1,X2,TOL)
	INTEGER ITMAX
	DOUBLE PRECISION ZBRENT,TOL,X1,X2,FUNC,EPS
	EXTERNAL FUNC
	PARAMETER (ITMAX=100, EPS=1.0D-15)
	INTEGER ITER
	DOUBLE PRECISION A,B,C,D,E,FA,FB,FC,P,Q,R,S,TOL1,XM

	A = X1
	B = X2
	FA = FUNC(A)
	FB = FUNC(B)
	IF ((FA .GT. 0. .AND. FB .GT. 0.) .OR.
     &		(FA .LT. 0. .AND. FB .LT. 0.)) THEN
		WRITE(*,*) 'ROOT MUST BE BRACKETED FOR ZBRENT'
		WRITE(*,*) 'X1,X2 =',X1,X2
		WRITE(*,*) 'F(X1),F(X2) =',FA,FB
		STOP
	END IF
	C = B
	FC = FB
	DO 11, ITER=1,ITMAX
		IF ((FB .GT. 0. .AND. FC .GT. 0.) .OR. 
     &			(FB .LT. 0. .AND. FC .LT. 0.)) THEN
			C = A
			FC = FA
			D = B - A
			E = D
		END IF
		IF (ABS(FC) .LT. ABS(FB)) THEN
			A = B
			B = C
			C = A
			FA = FB
			FB = FC
			FC = FA
		END IF
C CONVERGENCE CHECK
		TOL1 = 2. * EPS * ABS(B) + 0.5 * TOL
		XM = 0.5 * (C - B)
		IF (ABS(XM) .LE. TOL1 .OR. FB .EQ. 0.) THEN
			ZBRENT = B
			RETURN
		END IF
		IF (ABS(E) .GE. TOL1 .AND. ABS(FA) .GT. ABS(FB)) THEN
C ATTEMPT INVERSE QUADRATIC INTERPOLATION
			S = FB / FA
			IF (A .EQ. C) THEN
				P = 2. * XM * S
				Q = 1.0D+00 - S
			ELSE
				Q = FA / FC
				R = FB / FC
				P = S * (2.*XM*Q*(Q-R) - (B-A)*(R-1.0D+00))
				Q = (Q-1.0D+00) * (R-1.0D+00) * (S-1.0D+00)
			END IF
			IF (P .GT. 0.) Q = -Q
C CHECK WHETHER IN BOUNDS
			P = ABS(P)
			IF (2.*P .LT. MIN(3.*XM*Q-ABS(TOL1*Q),ABS(E*Q))) THEN
C ACCEPT INTERPOLATION
				E = D
				D = P / Q
			ELSE
C INTERPOLATION FAILED, USE BISECTION
				D = XM
				E = D
			END IF
		ELSE
C BOUNDS DECREASING TOO SLOWLY, USE BISECTION
			D = XM
			E = D
		END IF
C MOVE LAST BEST GUESS TO A
		A = B
		FA = FB
C EVALUEATE NEW TRIAL ROOT
		IF (ABS(D) .GT. TOL1) THEN
			B = B + D
		ELSE
			B = B + DSIGN(TOL1,XM)
		END IF
		FB = FUNC(B)
11	CONTINUE
	
	WRITE(*,*) 'ZBRENT EXCEEDED MAXIMUM ITERATIONS'
	ZBRENT = B

	RETURN
	END

C **********************************************************
C ROUTINE TO BRACKET ROOT OF FUNCTIONS, TO GIVE INPUT TO
C BRENT'S METHOD TO FIND THE ROOT OF A FUNCTION FUNC
C SINGLE-SIDED SEARCH, WITH XMIN AS LOWER LIMIT ALLOWED
	SUBROUTINE BRKTMIN(FUNC,XMIN,XNOM,X1,X2)
	DOUBLE PRECISION XMIN,XNOM,X1,X2,FUNC
	EXTERNAL FUNC
	DOUBLE PRECISION A,B,FA,FB

	A = MAX(XMIN,XNOM*.85)
	B = A * 1.02
	FA = FUNC(A)

	DO 10, ICNT=1,20
		FB = FUNC(B)
		IF ((FA .GT. 0. .AND. FB .LT. 0.) .OR. 
     &			(FA .LT. 0. .AND. FB .GT. 0.)) THEN
			X1 = A
			X2 = B
			RETURN
		END IF
		B = B * 1.02
10	CONTINUE
	WRITE (*,*) 'TOO MANY BRACKET ITERATIONS WITH XMIN'
	B = A * 1.02
	FB = FUNC(B)
	WRITE (*,*) 'A,B=',A,B
	WRITE (*,*) 'FA,FB=',FA,FB
	STOP
	END

C **********************************************************
C ROUTINE TO BRACKET ROOT OF FUNCTIONS, TO GIVE INPUT TO
C BRENT'S METHOD TO FIND THE ROOT OF A FUNCTION FUNC
C BASED ON USING A NOMINAL X AS STARTING POINT
C IS RECURSIVE
	SUBROUTINE BRACKET(FUNC,XNOM,X1,X2)
	DOUBLE PRECISION XNOM,X1,X2,FUNC
	DOUBLE PRECISION A,B,C,FA,FB,FC
	EXTERNAL FUNC
	
	IFLAG = 0

	IF (XNOM .EQ. 0.0) THEN
		A = -1.0
		B = 1.0
	ELSE
		A = XNOM * 0.9999
		B = XNOM * 1.02
	END IF
	FA = FUNC(A)
	FB = FUNC(B)
	
20	CONTINUE
	
	IF ((FA .GT. 0. .AND. FB .LT. 0.) .OR. 
     &		(FA .LT. 0. .AND. FB .GT. 0.)) THEN
		X1 = B
		X2 = A
		RETURN
	END IF
C PICK A NEW POINT WITH LINEAR INTERPOLATION + 2%
	DO 10, ICNT=1,20
		C = B - 1.02 * (A-B) * FB / (FA-FB)
		FC = FUNC(C)
		IF ((FA .GT. 0. .AND. FC .LT. 0.) .OR. 
     &			(FA .LT. 0. .AND. FC .GT. 0.)) THEN
			IF (ABS(A-C) .LT. ABS(B-C)) THEN
				X1 = C
				X2 = A
				RETURN
			ELSE
				X1 = C
				X2 = B
				RETURN
			END IF
		END IF
C THROW AWAY POINT FARTHEST FROM NEW POINT, AND TRY AGAIN
		IF (ABS(A-C) .LT. ABS(B-C)) THEN
			B = C
			FB = FC
		ELSE
			A = C
			FA = FC
		END IF

		IF (IFLAG .EQ. 1) THEN
			WRITE(*,*) A,FA
			WRITE(*,*) B,FB
		END IF

10	CONTINUE

C TOO MANY BRACKET ITERATIONS
	A = XNOM * 10.000
	B = XNOM * (-10.00)
	FA = FUNC(A)
	FB = FUNC(B)
	IFLAG = IFLAG+1
	IF (IFLAG .EQ. 1) THEN
		GOTO 20
	ELSE
		WRITE(*,*) 'TOO MANY BRACKET ITERATIONS'
		WRITE(*,*) 'A,B=',A,B
		WRITE(*,*) 'FA,FB=',FA,FB
		STOP
	END IF
	
	RETURN
	END

C **********************************************************
C FUNCTION FOR IMPLICIT HEAT CONDUCTION BETWEEN ZONES
C ISURF AND (ISURF+1), TO BE SOLVED WITH BRENT'S METHOD
C RETURNS DIFFERENCE BETWEEN HEAT CONDUCTION (INPUT)
C AND FOURIER CONDUCTION BASED ON TEMPS AT END OF TIME STEP
C HPN & HEN ARE ENERGIES AT STEP N, REST OF H & T AT N+1
	FUNCTION CONDSURF(HEAST)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	COMMON/SURFC/HPN,HEN,ZMP,ZME,DT,QMIN,CONDP,DX,BETAC,TP
	COMMON/SURFT/HGP,QUAL

C COMPUTE NEW ZONE ENERGIES BASED ON CURRENT HEAST
	HP = HPN - HEAST * DT
	HE = HEN + HEAST * DT
	HGP = HP / ZMP
	HGE = HE / ZME
C UPDATE TEMPERATURES
	CALL EOSURFT(QMIN,TP)
	CALL H2T(HGE,TE)
C COMPUTE FOURIER CONDUCTION BASED ON NEW TEMPS
	HEASTNEW = (TP - TE) * CONDP * BETAC / DX

	CONDSURF = HEASTNEW - HEAST
	RETURN
	END

C **********************************************************
C BUBBLE NUCLEATION RATE ROUTINE
C FROM CAREY, EQN 5.74
C MATERIAL PROPERTIES HARDWIRED FOR AL,ALUMINA,SILICA
C UNITS: J in m-3.s-1, SIGMA in N/m
	SUBROUTINE NUCLEATE(T,RHO,P,J,TSTAR)
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	DOUBLE PRECISION J,ISTAR
	DIMENSION ACOND(13),AH2T(10),EOSCOEF(10),SIGMACOEF(7)
	DIMENSION YIELDCOEF(3)

	COMMON/MATDAT/TMELT,HMELTI,HMELTT,ACOND,AH2T
	COMMON/EOSDAT/RHO0,EOSCOEF,ABOIL,BBOIL,PRNU,YIELDCOEF
	COMMON/SURFTEN/SIGMACOEF

	RBAR = EOSCOEF(10)
	JFLAG = NINT(SIGMACOEF(1))

	IF (JFLAG .EQ. 0) THEN
C NO MATERIAL SURFACE TENSION INFORMATION -> SKIP J CALCULATION
		J = 2.0D-97
		TSTAR = 0.1
	ELSE 
		IF (JFLAG .EQ. 1) THEN
C FIT PARAMETERS FOR FIT TO MURR (CUBIC EQUATION)
			A = SIGMACOEF(2)
			TCRIT = SIGMACOEF(3)
			SIGMA0 = SIGMACOEF(4)
			ASIG = SIGMACOEF(5)
			BSIG = SIGMACOEF(6)
			CSIG = SIGMACOEF(7)
		ELSE IF (JFLAG .EQ. 2) THEN
C FIT PARAMETERS FOR EXPONENTIAL EQUATION
			A = SIGMACOEF(2)
			TCRIT = SIGMACOEF(3)
			AEXP = SIGMACOEF(4)
			BEXP = SIGMACOEF(5)
		ELSE
			WRITE(*,*) 'INVALID MATERIAL (NUCLEATE)'
			STOP
		END IF

		AVOGADRO = 6.022D+26
		GAMMAE = 0.5772
		BOLTZ = 1.38D-23
		PI = 4.*ATAN(1.)

		IF (T .LT. TMELT) THEN
			WRITE(*,*) 'T OUTSIDE RANGE FOR NUCLEATE'
			J = 1.0D-98
			TSTAR = 0.1
		ELSE IF (T .GE. TCRIT) THEN
			IF (P .GT. 0.) THEN
				J = 1.0D-96
				TSTAR = 0.1
			ELSE
				J = 1.0D+43
				TSTAR = 1.0D-19
			END IF
		ELSE
C COMPUTE SATURATION PRESSURE
			PSAT = (10.**(ABOIL - BBOIL/T))

			IF (P .GT. PSAT) THEN
				J = 1.0D-96
				TSTAR = 0.1
			ELSE
C COMPUTE SURFACE TENSION
				IF (JFLAG .EQ. 2) THEN
C USE EXPONENTIAL FIT EQUATION
					SIGMA = AEXP * 10.**(BEXP * T)
				ELSE
C CUBIC SPLINE FIT TO DATA AT MELT TEMP AND ZERO AT CRIT
					T1 = (T - TMELT)
					SIGMA = SIGMA0 + T1*(ASIG+T1*(BSIG+T1*CSIG))
				END IF
		
				ETA = DEXP((P-PSAT)/(RHO*RBAR*T))
				X1 = -1.8195D+24 * (SIGMA**3)/(T*(ETA*PSAT-P)**2)
				X2 = SQRT(RHO**2 * SIGMA / A)

				J = 1.44D+40 * X2 * DEXP(X1)
				J = MAX(J,1.0D-97)
			END IF

C COMPUTE INDUCTION TIME REQUIRED TO REACH STEADY J-VALUE
C FROM MODIFICATION OF VAPOR CONDENSATION MEO BY WILEMSKI
C RSTAR IS THE CRITICAL BUBBLE RADIUS (meters)
			RSTAR = 2. * SIGMA / (ETA*PSAT - P)
			IF (RSTAR .LE. 0.) THEN
				TSTAR = 1.0D-02
			ELSE
				U = SQRT(8.*RBAR*T/PI)
				RHOV = PSAT / (RBAR * T)
				SMALLM = A / AVOGADRO
				SMALLN = ETA * PSAT / (BOLTZ * T)
				ISTAR = (4./3.) * PI * RSTAR**3 * RHOV / SMALLM
				WSTAR = (16.*PI/2.)*SIGMA**3/(BOLTZ*T*(ETA*PSAT-P)**2)
				TSTAR = RSTAR*ISTAR*RHOV / (U*SMALLM*SMALLN*WSTAR) * 
     &					(DLOG(WSTAR/3.) + GAMMAE)
				IF (TSTAR .LE. 0.) TSTAR = 1.0D-18
			END IF
		END IF
	END IF
	
101	FORMAT(7(1PE12.3))

	RETURN
	ENDR = 2. * SIGMA / (ETA*PSAT - P)
			IF (RSTAR .LE. 0.) THEN
				TSTAR = 1.0D-02
			ELSE
				U = SQRT(8.*RBAR*T/PI)
				RHOV = PSAT / (RBAR * T)
				SMALLM = A / AVOGADRO
				SMALLN = ETA * PSAT / (BOLTZ * T)
				ISTAR = (4./3.) * PI * RSTAR**3 * RHOV / SMALLM
				WSTAR = (16.*PI/2.)*SIGMA**3/(BOLTZ*T*(ETA*PSAT-P)**2)
				TSTAR = RSTAR*ISTAR*RHOV / (U*SMALLM*SMALLN*WSTAR) * 
     &					(DLOG(WSTAR/3.) + GAMMAE)
				IF (TSTAR .LE. 0.) TSTAR = 1.0D-18
			END IF
		END 